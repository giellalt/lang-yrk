!! Noun inflection
!! ---------------

!! Nenets nouns inflect in cases.


LEXICON N_ӇЭ  !!= @CODE@ ӈэ:ӈэ 1  !!ProsSg -вна
!! no change in stem
!! ңэ ŋæ     ңэн’ ŋæn°h	ңэ” ŋæq	ңэ ŋæ
!! * Yaml: __xo__
+N: NMN_ӇЭ ;
+N+Pl+Pros:%>ˮА1мна	K ; !2013-10-22
+N: POSSESSA-PL-PROS_ˮмана ;

LEXICON N_ӇЭ-Pal/Var  !!= @CODE@ ӈэ:ӈэ 1  !!ProsSg -вна
!! no change in stem
!! ңэ ŋæ     ңэн’ ŋæn°h	ңэ” ŋæq	ңэ ŋæ
+N: NMN_ӇЭ ;

LEXICON N_ӇЭ/ХАБИЕ  !!= @CODE@ ӈэ:ӈэ 1  !!ProsSg -вна
 !!= @CODE@ хӑби:8 !!ProsSg -вна
+N: NMN_ӇЭ ;
+N: PLACCSUF_Е ; !

LEXICON N_Ё  !!= @CODE@ я:я 2 !!ProsSg -вна
!! * Yaml: __ya__
!! [total=3]
!! я ya	ян’ yan°h	я” yaq	ё yo
+N: NMN_Ё ;

LEXICON N_ӇЭ/Ё !! та:та 
!!= @CODE@ ӈэ:ӈэ 1  !!ProsSg -вна
!!= @CODE@ я:я 2 !!ProsSg -вна
+N: NMN_ӇЭ/Ё ;

LEXICON N_ПИ  !!= @CODE@ пя:пя 3 !!ProsSg -вна
!! * Yaml: __pyi__
!! [total=3]
!! пя pya	пян’ pyan°h	пя” pyaq	пи pyí
+N: NMN_ПИ ;

LEXICON N_ТЫ  !!= @CODE@ ты: 4 !!ProsSg -вна
!! * Yaml: __ty__
!! [total=4]
!! ты ti	тэ̇н’ ten°h	ты” tiq	ты tí
+N: NMN_ТЫ ;

LEXICON N_ХАВО  !!= @CODE@ ха: 5 !!ProsSg -вна
!! [total=1]
!! ха xa	хан’ xan°h	ха” xaq	хаво xawo
+N: NMN_ХАВО ;


LEXICON N_СЁЁ  !!= @CODE@ сё: 6 !!ProsSg -вна
!! [total=0]
!! сё syo	сён’ syon°h	сё” syoq	сёё syoyo
+N: NMN_СЁЁ ;

LEXICON N_ИБЕ  !!= @CODE@ и: 7 !!ProsSg -вна
!! [total=0]
!! и yí	ин’ yín°h	и” yíq	ибе yíbye
+N: NMN_ИБЕ  ;

LEXICON N_ХАБИЕ  !!= @CODE@ хӑби:8 !!ProsSg -вна
!! [total=0]
!! хӑби xəbyi	хӑбин’ xəbyin°h	хӑби” xəbyiq	хӑбие xəbyiy
+N: NMN_ХАБИЕ  ;

LEXICON N_ӇЭ/СЁЁ/ХАБИЕ 
+N: NMN_ӇЭ/СЁЁ/ХАБИЕ ;

LEXICON N_ПАНЫ  !!= @CODE@ пӑны:пӑны 9 !!ProsSg -(э)вна
!! [total=30]
!! пӑны pəni°	пӑнэн’ pənen°h	пӑны” pəni°q	пӑны pəni°
+N: NMN_ПАНЫ ;

LEXICON N_ХУСУВЭЙ  !!= @CODE@ хусувэй: 10 !!ProsSg -ювна
!! [total=17]
!! хусувэй xusuwey°	хусувэн’ xusuwen°h	хусувэй” xusuwey°q	хусувэй xusuwey°
+N: NMN_ХУСУВЭЙ ;

LEXICON N_ЕД/ХУСУВЭЙ
+N: NMN_ЕД/ХУСУВЭЙ ;

LEXICON N_ХАНО  !!= @CODE@ хӑн: 11 !!ProsSg -увна
!! * Yaml: __xano__
!! [total=27]
!! хӑн xən°	хӑнӑн’ xənən°h	хӑн” xən°q	хӑно xəno
 N_ХАНО-SG ;
 N_ХАНО-PL ;

LEXICON N_ХАНО-SG
+N: NMN_ХАНО-SG ;
LEXICON N_ХАНО-PL
+N: NMN_ХАНО-PL ;

LEXICON N_ТИРЕ-RUS  
+Rus: N_ТИРЕ ;

LEXICON N_ТИРЕ  !!= @CODE@ тир: 12 !!ProsSg -увна
!! [total=3]
!! тир tyír°	тирӑн’ tyírən°h	тир” tyír°q	тире tyírye
+N: NMN_ТИРЕ ;

LEXICON N_ЕД-RUS
+Rus: N_ЕД ;
LEXICON N_ЕД  !!= @CODE@ ед: 13 !!ProsSg -увна /-ювна
!! [total=98]
!! ед yed°	едӑн’ yedən°h	ед” yed°q	ед yed°
+N: NMN_ЕД ;

LEXICON N_ТИРЕ/ХАНО
+N: NMN_ТИРЕ/ХАНО ;

LEXICON N_ЕД/ХАНО
+N: SGDATSUF ;
+N:а  SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N:  PLNOMSUF ;
+N: PLACCSUF_Zero ; !

LEXICON N_ЕД/ТИРЕ
+N: NMN_ЕД/ТИРЕ ;
LEXICON NMN_ЕД/ТИРЕ
 SGDATSUF ;
:а  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
 PLACCSUF_Zero ; !

LEXICON N_ЕД/ТИРЕ/ХАНО
+N: SGDATSUF ;
+N:а  SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N:  PLNOMSUF ;
+N: PLACCSUF_Zero ; !

LEXICON N_МАРАӇГЫ-RUS
+Rus: N_МАРАӇГЫ ;
LEXICON N_МАРАӇГЫ  !!= @CODE@ мӑраӈга: 14 !!ProsSg -вна
!! [total=15]
!! мӑраңга məraŋka	мӑраңган’ məraŋkan°h	мӑраңга” məraŋkaq	мӑраңгы məraŋki
 N_МАРАӇГЫ-SG ;
 N_МАРАӇГЫ-PL ;

LEXICON N_МАРАӇГЫ-SG
+N:	NMN_МАРАӇГЫ-SG ;
LEXICON N_МАРАӇГЫ-PL
+N:	NMN_МАРАӇГЫ-PL ;

LEXICON N_ӇУДИ-RUS 
+Rus: N_ӇУДИ ;

LEXICON N_ӇУДИ/ӇЭ
+N: NMN_ӇУДИ ;
+N: NMN_ӇЭ ;

LEXICON N_ӇУДИ  !!= @CODE@ ӈуда: 15 !!ProsSg -вна
!! [total=53]
!! ңуда ŋuda	ңудан’ ŋudan°h	ңуда” ŋudaq	ңуди ŋudyi
+N: NMN_ӇУДИ ;

LEXICON NMN_ӇУДИ  !!= @CODE@ ӈуда: 15 !!ProsSg -вна
!! * Yaml: __ngudi__
    SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
:%^A2I PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ЕСИ  !!= @CODE@ ая̆ха: 16 !!ProsSg -вна
!! [total=1]
!! я̆ха yəxa	я̆хан’ yəxan°h	я̆ха” yəxaq	еси yesy
+N: SGDATSUF ;
+N:  SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N:  PLNOMSUF ;
!+N:%^A2I PLACCSUF_Zero ; !

LEXICON N_ӇОДИ  !!= @CODE@ ӈодя: 17 !!ProsSg -вна
!! [total=28]
!! ңодя ŋodya	ңодян’ ŋodyan°h	ңодя” ŋodyaq	ңоди ŋodyi
+N: SGDATSUF ;
+N:  SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N:  PLNOMSUF ;
+N:%^A2I PLACCSUF_Zero ;
+N:  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ӇОДИ/ТЁН
!!
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_I ;
+N:%^PalLoss PLACCSUF_Minus ;

LEXICON N_ӇОДИ/ПИСЬ
!!
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_I ;
+N: PLACCSUF_Minus ;

LEXICON N_ЯЛЭ  !!= @CODE@ яля: 18 !!ProsSg -вна
!! [total=2]
!! яля yalya	ялян’ yalyan°h	яля” yalyaq	ялэ yale
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N:%^HardFronting PLACCSUF_Zero ;

LEXICON N_ХОБ-RUS
+Rus: N_ХОБ ;

LEXICON N_ХОБ  !!= @CODE@ хоба:хоба 19 !!ProsSg -вна
!! * Yaml: __xob__
!! [total=25]
!! хоба xoba	хобан’ xoban°h	хоба” xobaq	хоб xob°
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_Minus ;
+N:  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ӇУДИ/ХОБ
!!
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_Minus ;


LEXICON N_МАРАӇГЫ/ХОБ !!
!!
+N: SGDATSUF ;
+N: SG-ACC-STEM	;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_Minus ;

LEXICON N_ТЁН  !!= @CODE@ тёня:тёня 20 !!ProsSg -вна
!! * Yaml: __tyon__
!! [total=14]
!!тёня tyonya	тёнян’ tyonyan°h	тёня” tyonyaq	тён tyon°
+N: SGDATSUF ;
+N: SG-ACC-STEM	;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N:%^PalLoss PLACCSUF_Minus ;
+N:  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ПИСЬ  !!= @CODE@ пися: 21 !!ProsSg -вна
!! [total=10]
!! пися pyísya	писян’ pyísyan°h пися” pyísyaq	пись pyísy°
+N: SGDATSUF ;
+N: SG-ACC-STEM	;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N: PLACCSUF_Minus ;
+N:  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ХАСЕВ  !!= @CODE@ хасава: 22 !!ProsSg -вна
!! [total=1]
!! хасава xasawa	хасаван’ xasawan°h	хасава” xasawaq	хасев xasyew°
+N: SGDATSUF ;
+N: SG-ACC-STEM	;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N:%^VowFronting PLACCSUF_Minus ;
+N:   POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ӇАНУ-RUS
+Rus: N_ӇАНУ ; 
LEXICON N_ӇАНУ  !!= @CODE@ ӈӑно: 23 !!ProsSg -вна
!! * Yaml: __nano__
!! [total=57]
!! ңӑно ŋəno	ңӑнон’ ŋənon°h	ңӑно” ŋənoq	ңӑну ŋənu
+N: NMN_ӇАНУ ;

LEXICON NMN_ӇАНУ  !!= @CODE@ ӈӑно: 23 !!ProsSg -вна
	NMN_ӇАНУ-SG ;
	NMN_ӇАНУ-PL ;
LEXICON NMN_ӇАНУ-SG  
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
LEXICON NMN_ӇАНУ-PL  
  PLNOMSUF ;
:%^VowRaise PLACCSUF_Zero ;
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ЯКЫ  !!= @CODE@ якэ: 24 !!ProsSg -вна
!! [total=4]
!! якэ yake	якэн’ yaken°h	якэ” yakeq	якы yaki
+N: SGDATSUF ;
+N: SG-ACC-STEM ;
+N:  SG-NOM-STEM ;
+N: PLNOMSUF ;
+N:%^VowRaise PLACCSUF_Zero ;

LEXICON N_ӇУВО  !!= @CODE@ ӈумʼ:ӈум 25 !!ProsSg -(м)на
!! * Yaml: __yam__
!! [total=3]
!! ңум’ ŋum	ңумд’ ŋumt°h	ңув” ŋuw°q	ңуво ŋuwo
!LEXICON N_ЯМ !! 25!ямʼ:я
+N:ʼ SGDATSUF ;
+N:%^MLenition SG-ACC-STEM ;
+N:ʼ SG-NOM-STEM ;

+N:%^MLenition PLNOMSUF ;
+N:%^MLenition PLACCSUF_О ;
+N:ʼ  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_НЮБЕ  !!= @CODE@ нюмʼ: 26  !!ProsSg -(м)на
!! [total=0]
!! нюм’ nyum	нюмд’ nyumt°h	нюв” nyuw°q	нюбе nyubye
NMN_НЮБЕ ;

LEXICON N_НЮБЕ-Pal/Var
NMN_НЮБЕ ;

LEXICON NMN_НЮБЕ  !!= @CODE@ нюмʼ: 26
+N:Н1 SGDATSUF ;
+N:Н1 SG-ACC-STEM ;
+N:Н1 SG-NOM-STEM ;
+N:%^MLenition PLNOMSUF ;
+N:%^MLenition PLACCSUF_Е ;

LEXICON N_МУНО  !!= @CODE@ муʼ: 27  !!ProsSg -мна
!! н [total=2]
!! му’(н) múh	мунд’ múnt°h	мун” mún°q	муно múno
+N: NMN_МУНО ;

LEXICON NMN_МУНО  !!= @CODE@ муʼ: 27  !!ProsSg -мна
:Н1 SGDATSUF ;
:Н1 SG-ACC-STEM ;
:Н1 SG-NOM-STEM ;
:Н1 PLNOMSUF ;
:Н1 PLACCSUF_О ;

LEXICON N_ПОЁ  !!= @CODE@ поʼ:по 28  !!ProsSg -мна
!! * Yaml: __poyo__
!! ң [total=2]
!! по’(ң) poh	понд’ pont°h	пой” poy°q	поё poyo
!! Sg+Acc+Px2 ja Px3 assimilation -нмд- > -нд-
+N:Ӈ1 SGDATSUF ;
+N:Ӈ1 SG-ACC-STEM ;
+N:Ӈ1 SG-NOM-STEM ;
+N:й PLNOMSUF ;
+N: PLACCSUF_Ё ;
+N:Ӈ1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ВЫӇО  !!= @CODE@ выʼ:вы 29  !!ProsSg -мна /-мана
!! ң [total=16]
!! вы’(ң) wíh	вы̄нд’ wínt°h	вы” wí°q	выңо wíŋo
+N:Ӈ1 SGDATSUF ;
+N:Ӈ1 SG-ACC-STEM ;
+N:Ӈ1 SG-NOM-STEM ;
+N:Ӈ1 PLNOMSUF ;
+N:Ӈ1 PLACCSUF_О ;
+N:Ӈ1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ИЛЪЕ  !!= @CODE@ илʼ:ил 30   !!ProsSg -мана
!! * Yaml: __ilje__
!! ң [total=13]
!! ил’(ң) yil°h	илӑнд’ yilənt°h	ила” yilə°q	илъе yil°ye
+N: NMN_ИЛЪЕ ;

LEXICON N_ИЛЪЕ-Pal/Var
+N: NMN_ИЛЪЕ ;

LEXICON NMN_ИЛЪЕ
:%>аӇ1 SGDATSUF ;
:Ӈ1 SG-ACC-STEM ;
!+N:аӇ1 SG-NOM-STEM ; !!= @CODE@ 2013-10-25
:Ӈ1 SGNOMSUF ; !!= @CODE@ 
:Ӈ1 SG-NOM-STEM ; !!= @CODE@ 
:а PLNOMSUF ;
:ъ PLACCSUF_Е ;
:Ӈ1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_НЕНЭЦИЕ  !!= @CODE@ ненэцьʼ:ненэць 30   !!ProsSg -мана
!! * Yaml: __nyenecyh__
!! ң [total=13]
!! ил’(ң) yil°h	илӑнд’ yilənt°h	ила” yilə°q	илъе yil°ye
+N:%>аӇ1 SGDATSUF ;
+N:%>аӇ1 SG-ACC-STEM ;
+N:Ӈ1 SGNOMSUF ; !!= @CODE@ 
+N:%>аӇ1 SG-NOM-STEM ; !!= @CODE@ 
+N:%>а PLNOMSUF ;
+N:%>и PLACCSUF_Е ;
+N:%>аӇ1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ПАХАЁ  !!= @CODE@ пӑхӑʼ: 31   !!ProsSg -мна
!! ң [total=2]
!! пӑхӑ’(ң) pəx°h	пӑхӑнд’ pəxənt°h	пӑха” pəxə°q	пӑхӑё pəx°yo
+N:Ӈ1 SGDATSUF ;
+N:аӇ1 SG-ACC-STEM ;
+N:аӇ1 SG-NOM-STEM ;
+N:аӇ1 PLNOMSUF ;
+N: PLACCSUF_Ё ;

LEXICON N_СЕРО  !!= @CODE@ серˮ: 32   !!ProsSg -мана / -мня
!! [total=16]
!! сер” syer	серт’ syert°h	сер” syer°q	серо syero
+N:Д1 SGDATSUF ;
+N:Д1 SG-ACC-STEM ;
+N:Д1 SG-NOM-STEM ;
+N:Д1 PLNOMSUF ;
+N:Д1 PLACCSUF_О ;

LEXICON N_ТАРЕ  !!= @CODE@ тӑрˮ:тар 33   !!ProsSg -мня
!! [total=0]
!! тӑр” tər	тӑрт’ tərt°h	тӑр” tər°q	тӑре tərye
+N: NMN_ТАРЕ ;

LEXICON N_ТАРЕ-Pal/Var !! Where is the variation?
+N: NMN_ТАРЕ ;

LEXICON NMN_ТАРЕ  !!= @CODE@ тӑрˮ:тар 33   !!ProsSg -мня
:Д1 SGDATSUF ;
:Д1 SG-ACC-STEM ;
:Д1 SG-NOM-STEM ;
:Д1 PLNOMSUF ;
:Д1 PLACCSUF_Е ;

LEXICON N_МАНСО  !!= @CODE@ мӑнˮ: 34   !!ProsSg -мна
!! [total=6]
!! мӑн”(с) mən°q	мӑнӑт’ mənət°h	мӑнӑс” mənəs°q	мӑнсо mən°so
+N:С1 SGDATSUF ;
+N:аС1 SG-ACC-STEM ;
+N:аС1 SG-NOM-STEM ;
+N:аС1 PLNOMSUF ;
+N:С1 PLACCSUF_О ;

LEXICON N_МЯДО  !!= @CODE@ мяˮ:мя 35   !!ProsSg -мна
!! [total=18]
!! мя”(д) myaq	мят’ myat°h	мяд” myad°q	мядо myado
+N: NMN_МЯДО ;

LEXICON N_ВАР/МЯДО
 N_МЯДО ;

LEXICON N_ВАР/ТАРЕ  !!= @CODE@ мяˮ:мя 35   !!ProsSg -мна
!!= @CODE@ тӑрˮ:тар 33   !!ProsSg -мня
    N_МЯДО ;
:Д1 PLACCSUF_Е ;

LEXICON NMN_ВАР  !!= @CODE@ мяˮ:мя 35   !!ProsSg -мна
!! * Yaml: __waro__
:Д1 SGDATSUF ;
:Д1 SG-ACC-STEM ;
:Д1 SG-NOM-STEM ;
:Д1 PLNOMSUF ;
 PLACCSUF_О ;
:Д1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_МЯДО  !!= @CODE@ мяˮ:мя 35   !!ProsSg -мна
!! * Yaml: __myado__
:Д1 SGDATSUF ;
:Д1 SG-ACC-STEM ;
:Д1 SG-NOM-STEM ;
:Д1 PLNOMSUF ;
:Д1 PLACCSUF_О ;
:Д1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ИДЕ  !!= @CODE@ иˮ: 36   !!ProsSg -мна
!! * Yaml: __yidye__
!! [total=1]
!! и”(д) yiq	ит’ yit°h	ид” yid°q	иде yidye
+N: NMN_ИДЕ ;

LEXICON N_ИДЕ-Pal/Var !! Where is the variation?
+N: NMN_ИДЕ ;

LEXICON NMN_ИДЕ !! Where is the variation?
:Д1 SGDATSUF ;
:аС1 SG-ACC-STEM ;
:аС1 SG-NOM-STEM ;
:Д1 PLNOMSUF ;
:Д1 PLACCSUF_Е ;
:Д1  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON N_ИДЕ/МЯДО
 N_ИДЕ ;
 N_МЯДО ;
 
LEXICON N_ТЮСЕ  !!= @CODE@ тюˮ:тюсе 
+N: NMN_ТЮСЕ ;

LEXICON NMN_ТЮСЕ
:С1 SGDATSUF ;
:С1 SG-ACC-STEM ;
:С1 SG-NOM-STEM ;
:С1 PLNOMSUF ;
:с PLACCSUF_Е ;
:С1 POSSESSA-PLURAL ;

LEXICON N_ТАСО/ТЮСЕ
 N_ТАСО ;
 N_ТЮСЕ ;

LEXICON N_ӇАВО 
+N: NMN_ӇАВО ;

LEXICON NMN_ӇАВО !!Provisional solution
:мгэ SGDATSUF ;
:мгэ SG-ACC-STEM ;
:мгэ SG-NOM-STEM ;
:мгэ PLNOMSUF ;
:в PLACCSUF_О ; !
:мгэ POSSESSA-PLURAL ;

LEXICON N_Vow
+TYÄ+N: K ;

!LEXICON N_ЕСЯ !! >> N_ӇОДИ/ПИСЬ = @CODE@ 
!+N: NMN_Vow ;

LEXICON	    N_САВНЕ !!= @CODE@ 
+TYÄ+N: K ;

!LEXICON	    N_СЁ !! >> N_ӇЭ/СЁЁ/ХАБИЕ= @CODE@ 
!+N: NMN_Vow ;

!LEXICON	    N_ӇЭСЫ !! >> N_ӇЭ= @CODE@ ӈэсы:ӈэсы
!+N: NMN_Vow ;

!LEXICON	    N_ЯКЭ !! >> N_ЯКЫ
!+N: NMN_Vow ;

LEXICON	    N_ПЕНЗЕРЕ !!= @CODE@ пензерˮ:пензер
 N_ТАРЕ ;

LEXICON	N_САБЦЬ  !!= @CODE@ сабць:сабць
 N_ТИРЕ ;

LEXICON N_МЕРЁ !!= @CODE@ мерё:мерё
 N_ӇАНУ ;


!! NMN
LEXICON NMN_ТИРЕ/ХАНО
 SGDATSUF ;
:а  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
 PLACCSUF_Zero ; !


LEXICON NMN_ӇЭ !!= @CODE@ ӈэ:ӈэ 1  !!ProsSg -вна
  SGDATSUF ;
  SG-ACC-STEM ; !!= @CODE@ SGACCSUF, SGGENSUF
  SG-NOM-STEM ; !!= @CODE@ SG[DAT,LOC, ABL,TRA,PRO] DU[NOM,ACC,GEN] PL[DAT,LOC,ABL,TRA] PX-s, NOMINAL-CONJUGATION
  PLNOMSUF ;  !!= @CODE@ 
  PLACCSUF_Zero ; !!= @CODE@ => PL-ACC_STEM
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_Ё   !!= @CODE@ я:я 2 !!ProsSg -вна
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ; 
  PLPROSSUF ;
  POSSESSA-PL-PROS_ˮмана ;
+Pl+Pros:%^A2O%>ˮмна K ;
:%^A2O PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ӇЭ/Ё
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
 PLACCSUF_Zero ; !!
:%^A2O PLACCSUF_Zero ; !!

LEXICON NMN_ПИ !!= @CODE@ пя: 3 !!ProsSg -вна
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
:%^A2I PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ТЫ  !!= @CODE@ ты:ты 4 !!ProsSg -вна
!! [total=4]
!! ты ti        тэ̇н’ ten°h     ты” tiq ты tí
:%^VowLower SGDATSUF ;
  SG-ACC-STEM ;
:%^VowLower  SG-NOM-STEM ;
  PLNOMSUF ;
+Pl+Pros:%^VowLower%>ˮэмна K ; !!= @CODE@ This will need schwa to surface
 PLACCSUF_Zero ; !
:%^VowLower  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ХАВО  !!= @CODE@ ха: 5 !!ProsSg -вна
!! [total=1]
!! ха xa        хан’ xan°h      ха” xaq хаво xawo
 SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
:в PLACCSUF_О ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_СЁЁ  !!= @CODE@ сё: 6 !!ProsSg -вна
!! [total=0]
!! сё syo       сён’ syon°h     сё” syoq        сёё syoyo
 SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
 PLACCSUF_Ё ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ИБЕ  !!= @CODE@ и: 7 !!ProsSg -вна
!! [total=0]
!! и yí ин’ yín°h       и” yíq  ибе yíbye
 SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
:б PLACCSUF_Е ; !

LEXICON NMN_ХАБИЕ  !!= @CODE@ хӑби:8 !!ProsSg -вна
!! [total=0]
!! хӑби xəbyi  хӑбин’ xəbyin°h        хӑби” xəbyiq   хӑбие xəbyiy
 SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
 PLACCSUF_Е ; !

LEXICON NMN_ӇЭ/СЁЁ/ХАБИЕ
 SGDATSUF ;
 SG-ACC-STEM ;
 SG-NOM-STEM ;
 PLNOMSUF ;
 PLACCSUF_Zero ; !
 PLACCSUF_Ё ; !
 PLACCSUF_Е ; !

LEXICON NMN_ПАНЫ !! 9
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
  PLACCSUF_Zero ; !

LEXICON NMN_ХУСУВЭЙ !!= @CODE@ хусувэй: 10 !!ProsSg -ювна
  SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
  PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ЕД/ХАНО
 SGDATSUF ;
:а SGDATSUF ;
 SG-ACC-STEM ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
  PLACCSUF_Zero ; !
  PLACCSUF_О ; !
  POSSESSA-PLURAL ;
:%>о  POSSESSA-PLURAL ;

LEXICON NMN_ЕД/ХУСУВЭЙ
 SGDATSUF ;
:%^JLoss SG-ACC-STEM ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
  PLACCSUF_Zero ; !

LEXICON NMN_ХАНО
	NMN_ХАНО-SG ;
	NMN_ХАНО-PL ;
LEXICON NMN_ХАНО-SG
:а SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
LEXICON NMN_ХАНО-PL
  PLNOMSUF ;
 PLACCSUF_О ; !
:%>о  POSSESSA-PLURAL ;


LEXICON NMN_ТИРЕ  !!= @CODE@ тир: 12 !!ProsSg -увна
SGDATSUF ;
:а  SG-ACC-STEM ;
  SG-NOM-STEM ;
  PLNOMSUF ;
  PLACCSUF_Е ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl

LEXICON NMN_ЕД  !!= @CODE@ ед: 13 !!ProsSg -увна /-ювна
!! * Yaml: __yed__
    SGDATSUF ;
:а  SG-ACC-STEM ;
    SG-NOM-STEM ;
    PLNOMSUF ;
    PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl


!! !NOMINALS "NMN"
!! !THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON    NMN_МАРАӇГЫ
    NMN_МАРАӇГЫ-SG ;
    NMN_МАРАӇГЫ-PL ;
LEXICON NMN_МАРАӇГЫ-SG
 SGDATSUF ;
  SG-ACC-STEM ;
  SG-NOM-STEM ;
LEXICON NMN_МАРАӇГЫ-PL
  PLNOMSUF ;
:%^A2I PLACCSUF_Zero ; !
  POSSESSA-PLURAL ; !!= @CODE@ +Pl+Dat, +Pl+Loc, +Pl+Abl



!!

 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;


LEXICON N_ЯЙ
! мея:мея
+N: NMN_ЯЙ ;

LEXICON NMN_ЯЙ
 SGNOMSUF ;
 SGACCSUF ;
 SGGENSUF ;
 SGDATSUF ;
 SGLOCSUF ;
 SGABLSUF ;
 SGPROSSUF ;
 SGTRASUF ;

 DUNOMSUF ;
 DUACCSUF ;
 DUGENSUF ;

 PLNOMSUF ;
 PLACCSUF_Zero ; !
 PLDATSUF ;
 PLLOCSUF ;
 PLABLSUF ;
 PLTRASUF ;

 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;


LEXICON N_СЕЛВ
! мея:мея
+N: NMN_СЕЛВ ;

LEXICON NMN_СЕЛВ
 SGNOMSUF ;
 SGACCSUF ;
 SGGENSUF ;
 SGDATSUF ;
 SGLOCSUF ;
 SGABLSUF ;
 SGPROSSUF ;
 SGTRASUF ;

 DUNOMSUF ;
 DUACCSUF ;
 DUGENSUF ;

 PLNOMSUF ;
 PLACCSUF_Zero ; !
 PLDATSUF ;
 PLLOCSUF ;
 PLABLSUF ;
 PLTRASUF ;

 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;

!LEXICON N-nAI
! SGNOMSUF ;
! SGACCSUF ;
! SGGENSUF ;
! SGDATSUF ;
! SGLOCSUF ;
! SGABLSUF ;
! SGPROSSUF ;
! SGTRASUF ;
!
! DUNOMSUF ;
! DUACCSUF ;
! DUGENSUF ;
!
! PLNOMSUF ;
! PLACCSUF_I ; !
! PLGENSUF_I ; !
! PLDATSUF ;
! PLLOCSUF ;
! PLABLSUF ;
! PLPROSSUF ;
! PLTRASUF ;



 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;

LEXICON N_ИБЯ
!ибя:ибя
+N: NMN_ИБЯ ;
LEXICON NMN_ИБЯ
 SGNOMSUF ;
 SGACCSUF ;
 SGGENSUF ;
 SGDATSUF ;
 SGLOCSUF ;
 SGABLSUF ;
 SGPROSSUF ;
 SGTRASUF ;

 DUNOMSUF ;
 DUACCSUF ;
 DUGENSUF ;

 PLNOMSUF ;
 PLACCSUF_Zero ; !
 PLDATSUF ;
 PLLOCSUF ;
 PLABLSUF ;
 PLTRASUF ;

 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;

LEXICON N_ТАСО
+N: NMN_ТАСО ;
LEXICON NMN_ТАСО
 SGNOMSUF ;
 SGACCSUF ;
 SGGENSUF ;
 SGDATSUF ;
 SGLOCSUF ;
 SGABLSUF ;
 SGPROSSUF ;
 SGTRASUF ;

 DUNOMSUF ;
 DUACCSUF ;
 DUGENSUF ;

 PLNOMSUF ;
 PLACCSUF_Zero ; !
 PLDATSUF ;
 PLLOCSUF ;
 PLABLSUF ;
 PLTRASUF ;

 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;

LEXICON N_Cons
+TYÄ+N: SGNOMSUF ;

LEXICON N_ConsO
!вык:вык
+TYÄ+N: SGNOMSUF ;


LEXICON N_ВАР
! варˮ:вар
+N: NMN_ВАР ;

! Non-nasal glottal stops
!2013-05-22!LEXICON NMN_МЯДО
!2013-05-22! SGNOMSUF ;
!2013-05-22! SGACCSUF ;
!2013-05-22! SGGENSUF ;
!2013-05-22! SGDATSUF ;
!2013-05-22! SGLOCSUF ;
!2013-05-22! SGABLSUF ;
!2013-05-22! SGPROSSUF ;
!2013-05-22! SGTRASUF ;
!2013-05-22!
!2013-05-22! DUNOMSUF ;
!2013-05-22! DUACCSUF ;
!2013-05-22! DUGENSUF ;
!2013-05-22!
!2013-05-22! PLNOMSUF ;
!2013-05-22! PLACCSUF ;
!2013-05-22! PLDATSUF ;
!2013-05-22! PLLOCSUF ;
!2013-05-22! PLABLSUF ;
!2013-05-22! PLTRASUF ;

! POSSESSOR-INDICES ;



!Work required
LEXICON N_ТЭНО 
+N: SGNOMSUF ;
+N: SGACCSUF ;
+N: SGGENSUF ;
+N: SGDATSUF ;
+N: SGLOCSUF ;
+N: SGABLSUF ;
+N: SGPROSSUF ; 
+N: SGTRASUF ;

+N: DUNOMSUF ;
+N: DUACCSUF ;
+N: DUGENSUF ;

+N: PLNOMSUF ;
+N: PLACCSUF ;
+N: PLDATSUF ;
+N: PLLOCSUF ;
+N: PLABLSUF ;
+N: PLTRASUF ;

+N: POSSESSOR-INDICES ; ! 
+N+Nom: NOMINAL-CONJUGATION ;

LEXICON N-tojo
+TYÄ+N: K ;


LEXICON N-xawo
+N: SGNOMSUF ;
+N:д SGACCSUF ;
+N:д SGGENSUF ;
+N:ˮ SGDATSUF ;
+N:ˮ SGLOCSUF ;
+N:ˮ SGABLSUF ;
+N:ˮ SGPROSSUF ;
+N: SGTRASUF ;

+N: DUNOMSUF ;
+N: DUACCSUF ;
+N: DUGENSUF ;

+N:д PLNOMSUF ;
+N:д PLACCSUF ;
+N: PLDATSUF ;
+N:ˮ PLLOCSUF ;
+N:ˮ PLABLSUF ;
!+N: PLTRASUF ;

+N+Nom: NOMINAL-CONJUGATION ;
+N: POSSESSOR-INDICES ; !


LEXICON N_ЁЛЬЦЕ
! ёльцьʼ:ёльцӇ1
+N: SGNOMSUF ;
+N: SGACCSUF ;
+N: SGGENSUF ;
+N: SGDATSUF ;
+N: SGLOCSUF ;
+N: SGABLSUF ;
+N: SGPROSSUF ;
+N: SGTRASUF ;

+N: PLNOMSUF ;
+N: PLACCSUF ;
+N: PLDATSUF ;
+N: PLLOCSUF ;
+N: PLABLSUF ;
+N: PLTRASUF ;

+N+Nom: NOMINAL-CONJUGATION ;
+N: POSSESSOR-INDICES ; !

LEXICON N-якэ
 N_ПАНЫ ;

LEXICON N-N
+N: SGNOMSUF ;
+N: SGACCSUF ;

!! The singular accusative stem
LEXICON SG-ACC-STEM
 SGACCSUF ;
 SGGENSUF ;

!LEXICON SG-DAT-STEM
LEXICON SG-NOM-STEM
 SGNOMSUF ;
 SGLOCSUF ;
 SGABLSUF ;
 SGPROSSUF ;
 DUNOMSUF ;
 DUACCSUF ;
 DUGENSUF ;

 PLDATSUF ;
 PLLOCSUF ;
 PLABLSUF ;
 POSSESSOR-INDICES ;
+Nom: NOMINAL-CONJUGATION ;
PRED-POSSESSA ; ! This is perhaps a derivation
 SGTRASUF ;




LEXICON SGNOMSUF
+Sg+Nom: K ;

LEXICON SGACCSUF
+Sg+Acc:%>мʼ K ;

LEXICON SGGENSUF
+Sg+Gen:%>ʼ K ;

LEXICON SGDATSUF
+Sg+Dat:%>нʼ K ;

LEXICON SGLOCSUF
+Sg+Loc:%>хА1на K ; !вэсакохона пэдарахана ӈамгэхэна

LEXICON SGABLSUF
+Sg+Abl:%>хА1д K ; !вэсакоход пэдарахад ӈамгэхэд

LEXICON SGPROSSUF
+Sg+Pros:%>%{увм%}А2на K ; 

LEXICON SGTRASUF 
+Sg+Tra:%>ӈэ K ;

! These must be checked
LEXICON DUNOMSUF 
+Du+Nom:%>хА1ʼ K ;
!+Du+Nom:%>хʼ K ;

LEXICON DUACCSUF
+Du+Acc:%>хА1ʼ K ;
!+Du+Acc:%>хʼ K ;

LEXICON DUGENSUF 
+Du+Gen:%>хА1ʼ K ;
!+Du+Gen:%>хʼ K ;

!! Start Plural
LEXICON PLNOMSUF
+Pl+Nom:%>ˮ K ;
 PLTRASUF ;

LEXICON PLACCSUF
+Pl+Acc:%>Ы1 K ; ! We need something to trigger а:и 0:о but retain ы:ы
+Pl+Acc:%>Ы1 PL-ACC_STEM ;

LEXICON PLACCSUF_Minus
:%^VowLoss PL-ACC_STEM ;

LEXICON PLACCSUF_Zero
! This has zero ending
!+Pl+Acc: K ;
 PL-ACC_STEM ;

LEXICON PLACCSUF_I
+Pl+Acc:%>и  K ; !Stem final е а я may change to и.
:%>и PL-ACC_STEM ;

LEXICON PLACCSUF_Е
+Pl+Acc:%>е K ;
:%>е PL-ACC_STEM ;

LEXICON PLACCSUF_Ё
+Pl+Acc:%>ё K ;
:%>ё PL-ACC_STEM ;

LEXICON PLACCSUF_О
!+Pl+Acc:%>о  K ; 
:%>о PL-ACC_STEM ;


!LEXICON PLACCSUF_AO
!+Pl+Acc:  K ; ! The rule "a:o in я" This will be pointed to PLACCSUF_Zero

!! What is assumed on the basis of the plural accusative
LEXICON PL-ACC_STEM
+Pl+Acc:	K	;
+Pl+Gen:%>ˮ	K	;
+Pl+Pros:%>ˮ%{увм%}А2на 	K	; !!= @CODE@ When does this take a vowel хоˮомна
 POSSESSA-PL-NOM ;
+Pl+Acc: POSSESSA-PL-ACC ;
+Pl+Gen:%>ˮ POSSESSA-PL-GEN ;
  POSSESSA-PL-PROS ;
!2013-04-22!+Pl+Pros:ˮмана  POSSESSA-PL-PROS ;



!LEXICON PLGENSUF
!+Pl+Gen:%>Ы1ˮ PL-ACC_STEM ;

!LEXICON PLGENSUF_Glot
!+Pl+Gen:%>ˮ PL-ACC_STEM ;
!+Pl+Gen:%>ˮ K ; 

!LEXICON PLGENSUF_I
!+Pl+Gen:%>и  K ; !Stem final е а я may change to и.

!LEXICON PLGENSUF_О
!+Pl+Gen:%>оˮ PL-ACC_STEM ;
!+Pl+Gen:%>оˮ K ;

!LEXICON PLGENSUF_AO
!+Pl+Acc:%>ё  K ; ! This is not suffix, it should replace stem я. Exception list?

LEXICON PLDATSUF
+Pl+Dat:%>хА1ˮ K ;
+Pl+Dat:%>хˮ K ;

LEXICON PLLOCSUF
+Pl+Loc:%>хА1ˮна K ;

LEXICON PLABLSUF
+Pl+Abl:%>хА1т K ;


LEXICON PLPROSSUF
!+Pl+Pros:%>Ы1ˮмана K ;
+Pl+Pros:%>ˮмна K ;
!+Pl+Pros:%>ˮмана PL-PROS_POSSESSA ;
!+Pl+Pros:%>ˮмна K ; !явоˮмна



LEXICON  PLTRASUF
+Pl+Tra:%>ˮӈэ K ;

!! Possessor Indices
LEXICON POSSESSOR-INDICES

 POSSESSA-SINGULAR ;
 POSSESSA-DUAL ;

LEXICON POSSESSA-SINGULAR
	POSSESSA-SG-NOM ;
	POSSESSA-SG-ACC ;
	POSSESSA-SG-GEN ;
	POSSESSA-SG-DAT ;
	POSSESSA-SG-LOC ;
	POSSESSA-SG-ABL ;
	POSSESSA-SG-PROS ;

LEXICON POSSESSA-SG-NOM
+Sg+Nom+PxSg1:%>%{вм%}   K ;
+Sg+Nom+PxSg1:%>ми   K ;
+Sg+Nom+PxSg2:%>%{рл%}   K ; ! (1961: 110) нюр, ӈэсял, мяˮал
+Sg+Nom+PxSg3:%>да  K ;
+Sg+Nom+PxDu1:%>миʼ K ;
+Sg+Nom+PxDu2:%>%{рл%}иʼ K ;
+Sg+Nom+PxDu3:%>диʼ K ;
+Sg+Nom+PxPl1:%>%{вм%}аˮ K ;
+Sg+Nom+PxPl2:%>%{рл%}аˮ K ; ! (1961: 110) даˮ but л after Glots and schwa
+Sg+Nom+PxPl3:%>доʼ K ;

+Sg+Nom+PxSg1+Der/Pr+V+Ind+Aor+ScSg:%>в   K ; !!= @CODE@ 2013-10-22 Should this also have +ScSg3 marking
+Sg+Nom+PxSg2+Der/Pr+V+Ind+Aor+ScSg:%>р   K ;
+Sg+Nom+PxSg3+Der/Pr+V+Ind+Aor+ScSg:%>да  K ;
+Sg+Nom+PxDu1+Der/Pr+V+Ind+Aor+ScSg:%>миʼ K ;
+Sg+Nom+PxDu2+Der/Pr+V+Ind+Aor+ScSg:%>риʼ K ;
+Sg+Nom+PxDu3+Der/Pr+V+Ind+Aor+ScSg:%>диʼ K ;
+Sg+Nom+PxPl1+Der/Pr+V+Ind+Aor+ScSg:%>ваˮ K ;
+Sg+Nom+PxPl2+Der/Pr+V+Ind+Aor+ScSg:%>раˮ K ;
+Sg+Nom+PxPl3+Der/Pr+V+Ind+Aor+ScSg:%>доʼ K ;

+Sg+Nom+PxSg1+Der/Pr+V+Ind+Prt1+ScSg:%>вась   K ;
+Sg+Nom+PxSg2+Der/Pr+V+Ind+Prt1+ScSg:%>рась   K ;
+Sg+Nom+PxSg3+Der/Pr+V+Ind+Prt1+ScSg:%>дась  K ;
+Sg+Nom+PxDu1+Der/Pr+V+Ind+Prt1+ScSg:%>минзь K ;
+Sg+Nom+PxDu2+Der/Pr+V+Ind+Prt1+ScSg:%>ринзь K ;
+Sg+Nom+PxDu3+Der/Pr+V+Ind+Prt1+ScSg:%>динзь K ;
+Sg+Nom+PxPl1+Der/Pr+V+Ind+Prt1+ScSg:%>ваць K ;
+Sg+Nom+PxPl2+Der/Pr+V+Ind+Prt1+ScSg:%>раць K ;
+Sg+Nom+PxPl3+Der/Pr+V+Ind+Prt1+ScSg:%>донзь K ;

LEXICON POSSESSA-SG-ACC
+Sg+Acc+PxSg1:%>в    K ;
+Sg+Acc+PxSg1:%>ми   K ;! (1961: 112) нюми, ӈэсями, мяˮми 
+Sg+Acc+PxSg2:%>мд   K ;
+Sg+Acc+PxSg3:%>мда  K ;
+Sg+Acc+PxDu1:%>миʼ  K ;
+Sg+Acc+PxDu2:%>мдиʼ K ;
+Sg+Acc+PxDu3:%>мдиʼ K ;
+Sg+Acc+PxPl1:%>%{вм%}аˮ  K ;
+Sg+Acc+PxPl2:%>мдаˮ K ;
+Sg+Acc+PxPl3:%>мдоʼ K ;

LEXICON POSSESSA-SG-GEN
+Sg+Gen+PxSg1:%>н    K ;
+Sg+Gen+PxSg1:%>ни   K ; ! (1961: 111) нюни, ӈэсяни, мяˮни
+Sg+Gen+PxSg2:%>нд   K ;
+Sg+Gen+PxSg3:%>нда  K ;
+Sg+Gen+PxDu1:%>ниʼ  K ;
+Sg+Gen+PxDu2:%>ндиʼ K ;
+Sg+Gen+PxDu3:%>ндиʼ K ;
+Sg+Gen+PxPl1:%>наˮ  K ;
+Sg+Gen+PxPl2:%>ндаˮ K ;
+Sg+Gen+PxPl3:%>ндоʼ K ;

LEXICON POSSESSA-SG-DAT
+Sg+Dat+PxSg1:%>хА1н    K ;
+Sg+Dat+PxSg1:%>хА1ни   K ;
+Sg+Dat+PxSg2:%>хА1нд   K ;
+Sg+Dat+PxSg3:%>хА1нда  K ;
+Sg+Dat+PxDu1:%>хА1ниʼ  K ;
+Sg+Dat+PxDu2:%>хА1ндиʼ K ;
+Sg+Dat+PxDu3:%>хА1ндиʼ K ;
+Sg+Dat+PxPl1:%>хА1наˮ  K ;
+Sg+Dat+PxPl2:%>хА1ндаˮ K ;
+Sg+Dat+PxPl3:%>хА1ндоʼ K ;

LEXICON POSSESSA-SG-LOC
+Sg+Loc+PxSg1:%>хА1нан    K ;
+Sg+Loc+PxSg1:%>хА1нани   K ;
+Sg+Loc+PxSg2:%>хА1нанд   K ;
+Sg+Loc+PxSg3:%>хА1нанда  K ;
+Sg+Loc+PxDu1:%>хА1наниʼ  K ;
+Sg+Loc+PxDu2:%>хА1нандиʼ K ;
+Sg+Loc+PxDu3:%>хА1нандиʼ K ;
+Sg+Loc+PxPl1:%>хА1нанаˮ  K ;
+Sg+Loc+PxPl2:%>хА1нандаˮ K ;
+Sg+Loc+PxPl3:%>хА1нандоʼ K ;

LEXICON POSSESSA-SG-ABL
+Sg+Abl+PxSg1:%>хА1дан    K ;
+Sg+Abl+PxSg1:%>хА1дани   K ;
+Sg+Abl+PxSg2:%>хА1данд   K ;
+Sg+Abl+PxSg3:%>хА1данда  K ;
+Sg+Abl+PxDu1:%>хА1даниʼ  K ;
+Sg+Abl+PxDu2:%>хА1дандиʼ K ;
+Sg+Abl+PxDu3:%>хА1дандиʼ K ;
+Sg+Abl+PxPl1:%>хА1данаˮ  K ;
+Sg+Abl+PxPl2:%>хА1дандаˮ K ;
+Sg+Abl+PxPl3:%>хА1дандоʼ K ;

LEXICON POSSESSA-SG-4-EVAS !!= @CODE@ ABL
+PxSg1:%>хА1дан    K ;
+PxSg1:%>хА1дани   K ;
+PxSg2:%>хА1данд   K ;
+PxSg3:%>хА1данда  K ;
+PxDu1:%>хА1даниʼ  K ;
+PxDu2:%>хА1дандиʼ K ;
+PxDu3:%>хА1дандиʼ K ;
+PxPl1:%>хА1данаˮ  K ;
+PxPl2:%>хА1дандаˮ K ;
+PxPl3:%>хА1дандоʼ K ;

LEXICON POSSESSA-SG-PROS
 ! 2012-04-11 Check this for N_Cons and N-D
 ! There might be reason for увна and мна varients
+Sg+Pros+PxSg1:%>%{увм%}А2нан     K ;
+Sg+Pros+PxSg1:%>%{увм%}А2нани    K ;
+Sg+Pros+PxSg2:%>%{увм%}А2нанд    K ;
+Sg+Pros+PxSg3:%>%{увм%}А2нанда   K ;
+Sg+Pros+PxDu1:%>%{увм%}А2наниʼ	  K ;
+Sg+Pros+PxDu2:%>%{увм%}А2нандиʼ	  K ;
+Sg+Pros+PxDu3:%>%{увм%}А2нандиʼ	  K ;
+Sg+Pros+PxPl1:%>%{увм%}А2нанаˮ   K ;
+Sg+Pros+PxPl2:%>%{увм%}А2нандаˮ  K ;
+Sg+Pros+PxPl3:%>%{увм%}А2нандоʼ  K ;
!! The next line in +Sg+Pros+PxDu2:%>%{увм%}А2нандиʼ must be removed
!!+Sg+Pros+PxDu2:%>%{увм%}А2нандиʼ  K ; !! This line must be removed 2013-03-20

LEXICON POSSESSA-SG-PROS_увна
+Sg+Pros+PxSg1:%>увнан     K ;
+Sg+Pros+PxSg1:%>увнани    K ;
+Sg+Pros+PxSg2:%>увнанд    K ;
+Sg+Pros+PxSg3:%>увнанда   K ;
+Sg+Pros+PxDu1:%>увнаниʼ   K ;
+Sg+Pros+PxDu2:%>увнандиʼ          K ;
+Sg+Pros+PxDu3:%>увнандиʼ          K ;
+Sg+Pros+PxPl1:%>увнанаˮ   K ;
+Sg+Pros+PxPl2:%>увнандаˮ  K ;
+Sg+Pros+PxPl3:%>увнандоʼ  K ;



LEXICON POSSESSA-DUAL
!! Dual possessa
+Du+Nom+PxSg1:%>хюн       K ;
+Du+Nom+PxSg2:%>хюд       K ;
+Du+Nom+PxSg3:%>хюда      K ;
+Du+Nom+PxDu1:%>хюниʼ     K ;
+Du+Nom+PxDu2:%>хюдиʼ     K ;
+Du+Nom+PxDu3:%>хюдиʼ     K ;
+Du+Nom+PxPl1:%>хюнаˮ     K ;
+Du+Nom+PxPl2:%>хюдаˮ     K ;
+Du+Nom+PxPl3:%>хюдоʼ     K  ;

+Du+Nom+PxSg1:%>хА1юн       K ;!хасавахаюн
+Du+Nom+PxSg2:%>хА1юд       K ; !хасавахаюд
+Du+Nom+PxSg3:%>хА1юда      K ;
+Du+Nom+PxDu1:%>хА1юниʼ     K ;
+Du+Nom+PxDu2:%>хА1юдиʼ     K ;
+Du+Nom+PxDu3:%>хА1юдиʼ     K ;
+Du+Nom+PxPl1:%>хА1юнаˮ     K ;
+Du+Nom+PxPl2:%>хА1юдаˮ     K ;
+Du+Nom+PxPl3:%>хА1юдоʼ     K  ;

+Du+Nom+PxSg1+Der/Pr+V+Ind+Aor+ScDu:%>хА1юн       K ;
+Du+Nom+PxSg2+Der/Pr+V+Ind+Aor+ScDu:%>хА1юд       K ;
+Du+Nom+PxSg3+Der/Pr+V+Ind+Aor+ScDu:%>хА1юда      K ;
+Du+Nom+PxDu1+Der/Pr+V+Ind+Aor+ScDu:%>хА1юниʼ     K ;
+Du+Nom+PxDu2+Der/Pr+V+Ind+Aor+ScDu:%>хА1юдиʼ     K ;
+Du+Nom+PxDu3+Der/Pr+V+Ind+Aor+ScDu:%>хА1юдиʼ     K ;
+Du+Nom+PxPl1+Der/Pr+V+Ind+Aor+ScDu:%>хА1юнаˮ     K ;
+Du+Nom+PxPl2+Der/Pr+V+Ind+Aor+ScDu:%>хА1юдаˮ     K ;
+Du+Nom+PxPl3+Der/Pr+V+Ind+Aor+ScDu:%>хА1юдоʼ     K  ;

+Du+Nom+PxSg1+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юнась       K ;
+Du+Nom+PxSg2+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдась       K ;
+Du+Nom+PxSg3+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдась      K ;
+Du+Nom+PxDu1+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юнинзь     K ;
+Du+Nom+PxDu2+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдинзь     K ;
+Du+Nom+PxDu3+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдинзь     K ;
+Du+Nom+PxPl1+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юнаць     K ;
+Du+Nom+PxPl2+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдаць     K ;
+Du+Nom+PxPl3+Der/Pr+V+Ind+Prt1+ScDu:%>хА1юдонзь     K  ;

+Du+Acc+PxSg1:%>хюн   K ;
+Du+Acc+PxSg2:%>хюд   K ;
+Du+Acc+PxSg3:%>хюда  K ;
+Du+Acc+PxDu1:%>хюниʼ K ;
+Du+Acc+PxDu2:%>хюдиʼ K ;
+Du+Acc+PxDu3:%>хюдиʼ K ;
+Du+Acc+PxPl1:%>хюнаˮ K ;
+Du+Acc+PxPl2:%>хюдаˮ K ;
+Du+Acc+PxPl3:%>хюдоʼ K ;

+Du+Acc+PxSg1:%>хА1юн   K ; !хасавахаюн
+Du+Acc+PxSg2:%>хА1юд   K ;
+Du+Acc+PxSg3:%>хА1юда  K ;
+Du+Acc+PxDu1:%>хА1юниʼ K ;
+Du+Acc+PxDu2:%>хА1юдиʼ K ;
+Du+Acc+PxDu3:%>хА1юдиʼ K ;
+Du+Acc+PxPl1:%>хА1юнаˮ K ;
+Du+Acc+PxPl2:%>хА1юдаˮ K ;
+Du+Acc+PxPl3:%>хА1юдоʼ K ;

+Du+Gen+PxSg1:%>хюн   K ;
+Du+Gen+PxSg2:%>хют   K ;
+Du+Gen+PxSg3:%>хюта  K ;
+Du+Gen+PxDu1:%>хюниʼ K ;
+Du+Gen+PxDu2:%>хютиʼ K ;
+Du+Gen+PxDu3:%>хютиʼ K ;
+Du+Gen+PxPl1:%>хюнаˮ K ;
+Du+Gen+PxPl2:%>хютаˮ K ;
+Du+Gen+PxPl3:%>хютоʼ K ;

+Du+Gen+PxSg1:%>хА1юн   K ;! хасавахаюн
+Du+Gen+PxSg2:%>хА1ют   K ;
+Du+Gen+PxSg3:%>хА1юта  K ;
+Du+Gen+PxDu1:%>хА1юниʼ K ;
+Du+Gen+PxDu2:%>хА1ютиʼ K ;
+Du+Gen+PxDu3:%>хА1ютиʼ K ;
+Du+Gen+PxPl1:%>хА1юнаˮ K ;
+Du+Gen+PxPl2:%>хА1ютаˮ K ;
+Du+Gen+PxPl3:%>хА1ютоʼ K ;

LEXICON POSSESSA-PLURAL
 POSSESSA-PL-NOM ;
 POSSESSA-PL-OTHER ;

LEXICON POSSESSA-PL-NOM
!! Plural Possessa
 PL-NOM-PXSG1 ;
 PL-NOM-PXSG2 ;
 PL-NOM-PXSG3 ;
 PL-NOM-PXDU1 ;
 PL-NOM-PXDU2 ;
 PL-NOM-PXDU3 ;
 PL-NOM-PXPL1 ;
 PL-NOM-PXPL2 ;
 PL-NOM-PXPL3 ;

LEXICON PRED-POSSESSA
!!= * @CODE@ Could +Sg+Nom+PxSg3:%>да be the basis?
 PRED-ACC-POSSESSA ;
 PRED-GEN-POSSESSA ;
 PRED-NOM-POSSESSA ;

LEXICON PRED-NOM-POSSESSA
+Pred+Nom+PxSg1:%>да%>в   K ;
+Pred+Nom+PxSg2:%>да%>р   K ;
+Pred+Nom+PxSg3:%>да  K ;
+Pred+Nom+PxDu1:%>да%>миʼ K ;
+Pred+Nom+PxDu2:%>да%>риʼ K ;
+Pred+Nom+PxDu3:%>да%>диʼ K ;
+Pred+Nom+PxPl1:%>да%>ваˮ K ;
+Pred+Nom+PxPl2:%>да%>раˮ K ;
+Pred+Nom+PxPl3:%>да%>доʼ K ;

LEXICON PRED-ACC-POSSESSA
+Pred+Acc+PxSg1:%>да%>в    K ;
+Pred+Acc+PxSg1:%>да%>ми   K ;
+Pred+Acc+PxSg2:%>да%>мд   K ;
+Pred+Acc+PxSg3:%>да%>мда  K ;
+Pred+Acc+PxDu1:%>да%>миʼ  K ;
+Pred+Acc+PxDu2:%>да%>мдиʼ K ;
+Pred+Acc+PxDu3:%>да%>мдиʼ K ;
+Pred+Acc+PxPl1:%>да%>ваˮ  K ;
+Pred+Acc+PxPl2:%>да%>мдаˮ K ;
+Pred+Acc+PxPl3:%>да%>мдоʼ K ;

LEXICON PRED-GEN-POSSESSA
+Pred+Gen+PxSg1:%>да%>н    K ;
+Pred+Gen+PxSg1:%>да%>ни   K ;
+Pred+Gen+PxSg2:%>да%>нд   K ;
+Pred+Gen+PxSg3:%>да%>нда  K ;
+Pred+Gen+PxDu1:%>да%>ниʼ  K ;
+Pred+Gen+PxDu2:%>да%>ндиʼ K ;
+Pred+Gen+PxDu3:%>да%>ндиʼ K ;
+Pred+Gen+PxPl1:%>да%>наˮ  K ;
+Pred+Gen+PxPl2:%>да%>ндаˮ K ;
+Pred+Gen+PxPl3:%>да%>ндоʼ K ;



LEXICON PL-NOM-PXSG1
+Pl+Nom+PxSg1:%>н   K ;
+Pl+Nom+PxSg1:%>ни   K ;
+Pl+Nom+PxSg1+Der/Pr+V+Ind+Aor+ScPl:%>н   K ;
+Pl+Nom+PxSg1+Der/Pr+V+Ind+Prt1+ScPl:%>нась   K ;

LEXICON PL-NOM-PXSG2
+Pl+Nom+PxSg2:%>д   K ;
+Pl+Nom+PxSg2+Der/Pr+V+Ind+Aor+ScPl:%>д   K ;
+Pl+Nom+PxSg2+Der/Pr+V+Ind+Prt1+ScPl:%>дась   K ;

LEXICON PL-NOM-PXSG3
+Pl+Nom+PxSg3:%>да  K ;
+Pl+Nom+PxSg3+Der/Pr+V+Ind+Aor+ScPl:%>да  K ;
+Pl+Nom+PxSg3+Der/Pr+V+Ind+Prt1+ScPl:%>дась  K ;

LEXICON PL-NOM-PXDU1
+Pl+Nom+PxDu1:%>ниʼ K ;
+Pl+Nom+PxDu1+Der/Pr+V+Ind+Aor+ScPl:%>ниʼ K ;
+Pl+Nom+PxDu1+Der/Pr+V+Ind+Prt1+ScPl:%>нинзь K ;

LEXICON PL-NOM-PXDU2
+Pl+Nom+PxDu2:%>диʼ K ;
+Pl+Nom+PxDu2+Der/Pr+V+Ind+Aor+ScPl:%>диʼ K ;
+Pl+Nom+PxDu2+Der/Pr+V+Ind+Prt1+ScPl:%>динзь K ;

LEXICON PL-NOM-PXDU3
+Pl+Nom+PxDu3:%>диʼ K ;
+Pl+Nom+PxDu3+Der/Pr+V+Ind+Aor+ScPl:%>диʼ K ;
+Pl+Nom+PxDu3+Der/Pr+V+Ind+Prt1+ScPl:%>динзь K ;

LEXICON PL-NOM-PXPL1
+Pl+Nom+PxPl1:%>наˮ K ;
+Pl+Nom+PxPl1+Der/Pr+V+Ind+Aor+ScPl:%>наˮ K ;
+Pl+Nom+PxPl1+Der/Pr+V+Ind+Prt1+ScPl:%>наць K ;

LEXICON PL-NOM-PXPL2
+Pl+Nom+PxPl2:%>даˮ K ;
+Pl+Nom+PxPl2+Der/Pr+V+Ind+Aor+ScPl:%>даˮ K ;
+Pl+Nom+PxPl2+Der/Pr+V+Ind+Prt1+ScPl:%>даць K ;

LEXICON PL-NOM-PXPL3
+Pl+Nom+PxPl3:%>доʼ K ;
+Pl+Nom+PxPl3+Der/Pr+V+Ind+Aor+ScPl:%>доʼ K ;
+Pl+Nom+PxPl3+Der/Pr+V+Ind+Prt1+ScPl:%>донзь K ;

!!+Pl+Gen+PxSg1:%>ˮн   K ; ! Check 2013-01-09 %>н
!!+Pl+Gen+PxSg1:%>ˮни  K ; ! Check 2013-01-09 %>н
!!+Pl+Gen+PxSg2:%>т    K ; ! _*_ˮд : т_
!!+Pl+Gen+PxSg3:%>та   K ; ! _*_ˮд : т_
!!+Pl+Gen+PxDu1:%>ˮниʼ K ;! Check 2013-01-09 %>ниʼ
!!+Pl+Gen+PxDu2:%>тиʼ  K ;! _*_ˮд : т_
!!+Pl+Gen+PxDu3:%>тиʼ  K ; ! _*_ˮд : т_
!!+Pl+Gen+PxPl1:%>ˮнаˮ K ; ! Check 2013-01-09 %>наˮ 
!!+Pl+Gen+PxPl2:%>таˮ  K ; ! _*_ˮд : т_
!!+Pl+Gen+PxPl3:%>тоʼ  K ; ! _*_ˮд : т_


LEXICON POSSESSA-PL-ACC
 POSSESSA-PL-ACC_PXSG1 ;
 POSSESSA-PL-ACCGEN_PXSG2 ;
 POSSESSA-PL-ACCGEN_PXSG3 ;
 POSSESSA-PL-ACCGEN_PXDU1 ;
 POSSESSA-PL-ACCGEN_PXDU2 ;
 POSSESSA-PL-ACCGEN_PXDU3 ;
 POSSESSA-PL-ACCGEN_PXPL1 ;
 POSSESSA-PL-ACCGEN_PXPL2 ;
 POSSESSA-PL-ACCGEN_PXPL3 ;

LEXICON POSSESSA-PL-GEN
 POSSESSA-PL-GEN_PXSG1 ;
 POSSESSA-PL-ACCGEN_PXSG2 ;
 POSSESSA-PL-ACCGEN_PXSG3 ;
 POSSESSA-PL-ACCGEN_PXDU1 ;
 POSSESSA-PL-ACCGEN_PXDU2 ;
 POSSESSA-PL-ACCGEN_PXDU3 ;
 POSSESSA-PL-ACCGEN_PXPL1 ;
 POSSESSA-PL-ACCGEN_PXPL2 ;
 POSSESSA-PL-ACCGEN_PXPL3 ;

!LEXICON POSSESSA-PL-PROS
! POSSESSA-PL-GEN_PXSG1 ;
!:ˮ POSSESSA-PL-ACCGEN_PXSG2 ;
!:ˮ POSSESSA-PL-ACCGEN_PXSG3 ;
! POSSESSA-PL-ACCGEN_PXDU1 ;
!:ˮ POSSESSA-PL-ACCGEN_PXDU2 ;
!:ˮ POSSESSA-PL-ACCGEN_PXDU3 ;
! POSSESSA-PL-ACCGEN_PXPL1 ;
!:ˮ POSSESSA-PL-ACCGEN_PXPL2 ;
!:ˮ POSSESSA-PL-ACCGEN_PXPL3 ;


LEXICON  POSSESSA-PL-ACCGEN_PXSG1
+PxSg1:%>н   K ;

LEXICON  POSSESSA-PL-ACC_PXSG1
+PxSg1:%>н   K ;
+PxSg1:%>ни   K ;

LEXICON  POSSESSA-PL-GEN_PXSG1
+PxSg1:%>н   K ;

LEXICON  POSSESSA-PL-ACCGEN_PXSG2
+PxSg2:%>д   K ;

LEXICON  POSSESSA-PL-ACCGEN_PXSG3
+PxSg3:%>да  K ;

LEXICON  POSSESSA-PL-ACCGEN_PXDU1
+PxDu1:%>ниʼ K ;

LEXICON  POSSESSA-PL-ACCGEN_PXDU2
+PxDu2:%>диʼ K ;

LEXICON  POSSESSA-PL-ACCGEN_PXDU3
+PxDu3:%>диʼ K ;

LEXICON  POSSESSA-PL-ACCGEN_PXPL1
+PxPl1:%>наˮ K ;

LEXICON  POSSESSA-PL-ACCGEN_PXPL2
+PxPl2:%>даˮ K ;

LEXICON  POSSESSA-PL-ACCGEN_PXPL3
+PxPl3:%>доʼ K ;



LEXICON POSSESSA-PL-OTHER
 POSSESSA-PL-DAT ;
 POSSESSA-PL-ABL ;
 POSSESSA-PL-LOC ;
 POSSESSA-PL-PROS ;

LEXICON POSSESSA-PL-DAT
+Pl+Dat+PxSg1:%>хА1ˮн   K ;
+Pl+Dat+PxSg2:%>хА1т    K ;
+Pl+Dat+PxSg3:%>хА1та   K ;
+Pl+Dat+PxDu1:%>хА1ˮниʼ K ;
+Pl+Dat+PxDu2:%>хА1тиʼ  K ;
+Pl+Dat+PxDu3:%>хА1тиʼ  K ;
+Pl+Dat+PxPl1:%>хА1ˮнаˮ K ;
+Pl+Dat+PxPl2:%>хА1таˮ  K ;
+Pl+Dat+PxPl3:%>хА1тоʼ  K ;

LEXICON POSSESSA-PL-LOC
+Pl+Loc+PxSg1:%>хА1ˮнан   K ;
+Pl+Loc+PxSg2:%>хА1ˮнат   K ;
+Pl+Loc+PxSg3:%>хА1ˮната  K ;
+Pl+Loc+PxDu1:%>хА1ˮнаниʼ K ;
+Pl+Loc+PxDu2:%>хА1ˮнатиʼ K ;
+Pl+Loc+PxDu3:%>хА1ˮнатиʼ K ;
+Pl+Loc+PxPl1:%>хА1ˮнанаˮ K ;
+Pl+Loc+PxPl2:%>хА1ˮнатаˮ K ;
+Pl+Loc+PxPl3:%>хА1ˮнатоʼ K ;

LEXICON POSSESSA-PL-ABL
+Pl+Abl+PxSg1:%>хА1тан   K ;
+Pl+Abl+PxSg1:%>хА1тани   K ;
+Pl+Abl+PxSg2:%>хА1тат   K ;
+Pl+Abl+PxSg3:%>хА1тата  K ;
+Pl+Abl+PxDu1:%>хА1таниʼ K ;
+Pl+Abl+PxDu2:%>хА1татиʼ K ;
+Pl+Abl+PxDu3:%>хА1татиʼ K ;
+Pl+Abl+PxPl1:%>хА1танаˮ K ;
+Pl+Abl+PxPl2:%>хА1татаˮ K ;
+Pl+Abl+PxPl3:%>хА1татоʼ K ;

LEXICON POSSESSA-PL-PROS
+Pl+Pros+PxSg1:%>ˮ%{увм%}А2нан   K ;
+Pl+Pros+PxSg2:%>ˮ%{увм%}А2нат   K ;
+Pl+Pros+PxSg3:%>ˮ%{увм%}А2ната  K ;
+Pl+Pros+PxDu1:%>ˮ%{увм%}А2наниʼ K ;
+Pl+Pros+PxDu2:%>ˮ%{увм%}А2натиʼ K ;
+Pl+Pros+PxDu3:%>ˮ%{увм%}А2натиʼ K ;
+Pl+Pros+PxPl1:%>ˮ%{увм%}А2нанаˮ K ;
+Pl+Pros+PxPl2:%>ˮ%{увм%}А2натаˮ K ;
+Pl+Pros+PxPl3:%>ˮ%{увм%}А2натоʼ K ;

LEXICON POSSESSA-PL-PROS_ˮмана
!!+Pl+Pros:ˮмана
+Pl+Pros+PxSg1:ˮмана%>н   K ;
+Pl+Pros+PxSg2:ˮмана%>т   K ;
+Pl+Pros+PxSg3:ˮмана%>та  K ;
+Pl+Pros+PxDu1:ˮмана%>ниʼ K ;
+Pl+Pros+PxDu2:ˮмана%>тиʼ K ;
+Pl+Pros+PxDu3:ˮмана%>тиʼ K ;
+Pl+Pros+PxPl1:ˮмана%>наˮ K ;
+Pl+Pros+PxPl2:ˮмана%>таˮ K ;
+Pl+Pros+PxPl3:ˮмана%>тоʼ K ;





!! Conjugation of nouns and adjectives
LEXICON NOMINAL-CONJUGATION
+Der/Pr+V+Ind+Aor+ScSg1:%>дмʼ K ;
+Der/Pr+V+Ind+Aor+ScSg1:%>мʼ K ; !хомʼ
+Der/Pr+V+Ind+Aor+ScSg2:%>н   K ;
+Der/Pr+V+Ind+Aor+ScSg3:      K ;
+Der/Pr+V+Ind+Aor+ScDu1:%>ниʼ K ;
+Der/Pr+V+Ind+Aor+ScDu2:%>диʼ K ;
+Der/Pr+V+Ind+Aor+ScDu3:%>хА1ʼ K ;
+Der/Pr+V+Ind+Aor+ScPl1:%>ваˮ K ;
+Der/Pr+V+Ind+Aor+ScPl2:%>даˮ K ;
+Der/Pr+V+Ind+Aor+ScPl3:%>ˮ   K ;

+Der/Pr+V+Ind+Prt1+ScSg1:%>дамзь K ;
+Der/Pr+V+Ind+Prt1+ScSg1:%>манзь K ; !хоманзь
+Der/Pr+V+Ind+Prt1+ScSg2:%>нась  K ;
+Der/Pr+V+Ind+Prt1+ScSg3:%>сь    K ;
+Der/Pr+V+Ind+Prt1+ScDu1:%>нинзь K ;
+Der/Pr+V+Ind+Prt1+ScDu2:%>динзь K ;
+Der/Pr+V+Ind+Prt1+ScDu3:%>хА1нзь K ;
+Der/Pr+V+Ind+Prt1+ScPl1:%>ваць  K ;
+Der/Pr+V+Ind+Prt1+ScPl2:%>даць  K ;
+Der/Pr+V+Ind+Prt1+ScPl3:%>ць    K ;



! vim: set ft=xfst-lexc:

