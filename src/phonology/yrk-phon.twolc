! Nenets twol file

Alphabet

! Letters of the alphabet
а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я
А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

 ˮ ! Non-nasal glottal
 ʼ ! Nasal glottal

! other symbols
   %- !0

! Archiphonemes for vowels
 А1:а
 А1:о
 А1:э

 Ы1:о 
 Ы1:е


! Archiphonemes for glottals
! Н1:ʼ
! Н1:н
 Н1:0
! Ӈ1:ʼ
! Ӈ1:ӈ
 Ӈ1:0
 Д1:ˮ
 Д1:д
 Д1:0
 С1:ˮ
 С1:с
 С1:0

!! !!triggers
%^A2O:0
%^A2I:0 
%^MLenition:0 !! lenition
%^VowRaise:0 !! vowel raising э:ы о:у

! Boundary symbols

 %>
 #

 ;



Sets
Vows =   о у ё ю О У Ё Ю ;
Ve =   ӓ а е и э я Ӓ А Е И Э Я ;
Vws =  Vo VO Ve ы Ы  ӹ Ӹ ; 

CnsNotGlots = б в г д ж з й к л м н п р с т ф х ц
              Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц
              ч ш щ
              Ч Ш Щ  ;
Glots = ˮ ʼ ;

Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;
Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш С ;

Ltrs = Vws Cns  ъ ь Ъ Ь ;

all =  Ltrs  %- ;




Definitions

LFV = [ е | ю | ё | и ] ;

LBV = [ а | я | [ #: | CnsNotGlots ] [ у | о | э | ы ] ] ;


Rules

"й:0 when followed by vowel in VowelPalatalization"
й:0 <=> _ %>: [ а: | у: ] ;

"а:я VowelPalatalization"
а:я <=> й: %>: _ ;

"у:ю VowelPalatalization"
у:ю <=> й: %>: _ ;

"А1 -> а"
А1:а <=> [[ Ӈ1: |  ʼ: | а | я | Cns | Д1: | С1: ] %>: х: | %> а х ] _ ;

"А1 -> о"
А1:о <=> [ о | ё ]  %> х _ ;

"А1 -> э"
А1:э <=> [ ы | э | е ]  %> х _ ;


"StemFinalYToE"
ы:э <=> _ %>: х э  ;

"Ы1:о"
Ы1:о <=> LBV [ CnsNotGlots | Glots | :ӈ | :н ] %>: _ ;

"Ы1:е"
Ы1:е <=> LFV [ CnsNotGlots | Glots | :ӈ | :н ]  %>: _ ; 

"a:o in я"
я:ё <=> _ %^A2O:0 ;

"a:i in пя"
я:и <=> _ %^A2I:0 ;

"о:у word-final position"
о:у <=> _ %^VowRaise:0 ;

"э:ы word-final position"
э:ы <=>		_ %^VowRaise:0 ;



!! Consonant changes

"mToV" ! perhaps there should be a VtoM rule2013-01-23
м:в <=> _ %^MLenition:0 [ # | %> [ о | м | ʼ | ˮ ] ] ;
!ʼ %>: [ Ы1: | ʼ | м ʼ ] ;

"mTob" ! 
м:б <=> _ %^MLenition:0 %> [ е ] ;


"v To m "
в:м <=> [ Ӈ1: ] %>: _ ;
! [ м: ʼ: | Ӈ1: ] %>: _ ;

"r To l after nasal glottal"
р:л <=> [ ʼ: | Ӈ1: ] %>: _ ;

"nTod"
н:д <=>  [ Ӈ1: | ʼ: ] %>: _ ʼ ;

"d to t after non-nasal glottal"
д:т <=> ˮ: %>: _ ;

"с => з after nasal"
с:з <=> [ м | н | Н1: | Ӈ1: | ʼ: ] %>: _ ;

"ʼ:0"
ʼ:0 <=> м:  _ %>: [ Ы1: | ʼ | м ( ʼ ) | н: ( ʼ ) | ˮ: | х: ( А1: ) | р: | в: | д | ц ь | с: ь ] ;

"ˮ:0"
ˮ:0 <=> _ %>: д: ;

"х -> г after nasal glottal"
х:г <=> [ ʼ: | Ӈ1: ] %>: _ ( А1: ) ;

"х to к after non-nasal glottal"
х:к <=> [ Д1: | С1: ] %>: _  ;

! Realization of glottal stops
!! SURFACE CONSONANT
"С1:с GlottalNonNasalToSurface"
С1:с <=> _ %>: [ м #: | ʼ #: | ˮ #: | Ы1: ] ;

"Д1:д GlottalNonNasalToSurface"
Д1:д <=> _ %>: [ м ( ʼ ) #: | ʼ #: | ˮ #: | Ы1: ] ;

"Н1:н GlottalNasalToSurface"
Н1:н <=> _ %>: Ы1: ;

"Ӈ1:ӈ GlottalNasalToSurface"
Ӈ1:ӈ <=> _ %>: [ Ы1: | х: | о] ;

"Ӈ1:н GlottalNasalToSurface"
Ӈ1:н <=> _ %>: [ с: | н:д | д | ц: ] ;


!! LOSS

!"v loss after m Nasal glottal"
!в:0 <=> м ʼ: %>: _ #: ;

"m loss before Labial followed by nonglottal cons or vow "
м:0 <=> _ ʼ: %>: [ м [ а | и | н | д ] | в: ] ;

!"Н1:0 GlottalNasalToSurface"
!Cx:0 <=> _ %>: [ м | ʼ ] ;
!       where Cx in (Н1 Ӈ1) ;

"Д1:0 GlottalNonNasalToZERO"
Д1:0 <=> _ %>:  х:  ;

"С1:0 GlottalNonNasalToZERO"
С1:0 <=> _ %>: [ х: | н ] ;


"ь:0 with  GlottalNonNasalToZERO"
ь:0 <=> С1: _ %>: [ х: | н ] ;
!!

"С1:ˮ UnderlyingToGlottal"
С1:ˮ <=> _ [ #: | %>: м н а ] ;

"Д1:ˮ UnderlyingToGlottal"
Д1:ˮ <=> _ [ #: | %> [ м н а | н и ]] ;

"Н1:ʼ UnderlyingToGlottal"
Cx:ʼ <=> _ #: ;
       where Cx in (Н1 Ӈ1) ;

