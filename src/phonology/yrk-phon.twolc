! Nenets twol file

Alphabet

! Letters of the alphabet
а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я
А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

 ˮ ! Non-nasal glottal
 ʼ ! Nasal glottal

! other symbols
   %- !0

! Archiphonemes for vowels

 %{ауоэØ%}:а !!= @CODE@  А1:а А1:у А1:о А1:э SCHWA
%{ауоэиыØ%}:0 !!= @CODE@ before pros

А2:а
!А2:0

Ы1:о 
Ы1:е

Ы2:э


! Archiphonemes for glottals
! Н1:ʼ
! Н1:н
Н1:0
! Ӈ1:ʼ
! Ӈ1:ӈ
 Ӈ1:0
! Д1:ˮ
! Д1:д
 Д1:0
 С1:ˮ
 С1:с
 С1:0

 %{аяуюØ%}:0	!!= @CODE@ хан+N+Sg+Acc+PxSg1: ханув, ханав
!хонась+V+Refl+Mod/int+RcSg3: хонасьˮ
 %{ая%}:а         !!= @CODE@ in Pros

 %{увм%}:0	   !!= @CODE@ +N+Sg+Pros

 %{вм%}:0	   !!= @CODE@ +N+Sg+Nom+PxSg1
 %{рл%}:0	   !!= @CODE@ +N+Sg+Nom+PxSg2

!! !!triggers
 %^A2O:0
 %^A2I:0 
 %^MLenition:0 !! lenition
 %^VowLower:0 !!= @CODE@ vowel lowering ы:э у:о
 %^VowRaise:0 !!= @CODE@ vowel raising э:ы о:у
 %^VowLoss:0 !! stem-final vowel is lost in plural accusative
 %^StemVowFronting:0 !! хасава:хасев
 %^VowFronting:0 !! хадась:хадэйнинзь
 %^PalLoss:0 !! in combination with stem-final vowel loss тёня:тён
 %^HardFronting:0 !! яля:ялэ
! Boundary symbols

 %>
 #

 ;



Sets
Vow =   а е ё и о у ы э ю я
        А Е Ё И О У Ы Э Ю Я ;

CnsNotGlots = б в г д ж з й к л м н п р с т ф х ц
              Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц
              ч ш щ
              Ч Ш Щ  ;
Glots = ˮ ʼ ;

Cns =  б в г д ж з к л м н ӈ п р с т ф х ц ч ш 
       Б В Г Д Ж З К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш ;
Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш С ;

Ltrs = Vow Cns  ъ ь Ъ Ь ;

all =  Ltrs  %- ;




Definitions

LFV = [ е | ю | ё | и ] ;

LBV = [ а | я | [ #: | CnsNotGlots ] [ у | о | э | ы ] ] ;

PAIRSYLL = [ Vow:+ Cns:+ Vow: Cns:* ] ;

Rules

"0:а between Д1: %> and  мна"
!! __@RULENAME@__
 0:а <=> Д1: %> _ м н а ;

"й:0 when followed by vowel in VowelPalatalization"
!! __@RULENAME@__
 й:0 <=> _ %> [ а: | у: ] ;

"а:я VowelPalatalization left"
!! __@RULENAME@__
 а:я <= [ й:0 | ь:0 ] %> _ ;

"а:я VowelPalatalization right"
!! __@RULENAME@__
 а:я => [ й:0 | ь:0 ] %> _ ;
        е %> н _ [ к [ ы | э: ] | р (а) х | # ] ;

"%{ая%}:я VowelPalatalization left"
!! __@RULENAME@__
 %{ая%}:я <= %> с _ %^VowRaise:0 %> ;

"%{ая%}:я VowelPalatalization right"
!! __@RULENAME@__
 %{ая%}:я =>  %> с _ %^VowRaise:0 %> ;
# [[ с | н | т ] [ е | и | ю ] | и ] [( Н1: | Ӈ1: | Д1: ) %> (ˮ)| %> ˮ ] %{ауоэиыØ%}: %{увм%}: (А2:) н _ ;
   # п э в а: ʼ: %> н: _ ;

"у:ю VowelPalatalization"
!! __@RULENAME@__
 у:ю <=> [ й: | ь: ]  %> _ ;

"А2:0 possible vowel loss as second syllable vowel"
!! __@RULENAME@__
 А2:0 <= # Cns* :Vow [ %> | Cns | ˮ ]+ %{увм%}: _ н а ; 
         :Vow %^MLenition:0 %> %{увм%}: _ н а ; 
         # Cns:* Vow:+ Ӈ1: (%^MLenition:0) %> %{увм%}: _ н а ; !! поʼ:помна
         [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> 0: %{увм%}: _ н а ; 
         # Cns* :Vow [( Н1: | Ӈ1: | Д1: | С1: )| Cns+ ( :0* у: ) | й: у: | Cns Vow ] %> %{ауоэиыØ%}: %{увм%}: _ н %{ая%}: ;

"0:U in sgprossuf"
!! __@RULENAME@__
 0:у <=> [ д | м: (ʼ: ) | н | р ] %> _ %{увм%}: А2: н а ;

"0:о between two glottals"
!! __@RULENAME@__
 0:о <=> С1: %> _ ˮ м а ;

"%{ауоэØ%} -> 0 right"
!! __@RULENAME@__
!! {{{
 %{ауоэØ%}:0 =>       # Cns* [ а | я | и | ы | у | ю ] [ Н1: | Ӈ1: | Д1: ] %> х: _ [ ʼ | н а | д а | т Vow | ˮ: н Vow ] ; 
 # Cns* [ о | э | е ] [ Н1: | Ӈ1: ] %> х: _ [ ( ˮ ) н а | т Vow ] ;
 # Cns* Vow [ Н1: | Ӈ1: ] %> д _ р е ; !!= @CODE@ odd-syllables
!! }}}
"%{ауоэØ%} <- 0 left"
!! __@RULENAME@__
 %{ауоэØ%}:0 <=  # Cns* [ а | я | и | ы | у | ю ] [ Н1: | Ӈ1: ] %> х: _ ʼ ; 
!      # Cns* [ а | я | и | ы | у | ю ] [ Н1: | Ӈ1: | Д1: ] %> х: _ ʼ ; 
! # Cns* [ о | э | е ] [ Н1: | Ӈ1: ] %> х: _ ( ˮ ) н а ;

"%{ауоэØ%} <- а left"
!! __@RULENAME@__
 %{ауоэØ%}:а <= [ Cns* [ а | я | о | у | ю | ы | и ] [ Н1: | Ӈ1: ]| %> с %{ая%}: ] %> х: _ [ Cns  | ˮ ] ;
!  # Cns* [ а | я | у | ю | ы | и ] [ Н1: | Ӈ1: | Д1: ] %> х: _ [ Cns  | ˮ ] ;
    [ %> | а ] я %> х _ ʼ ;
                   [[ [ Cns | # Cns* ] [ :а | :я | й | и | ы | Ы2:ы ] | Cns ] %> х: | %> а х ] _ ;
!                   [[ а | я | й | и | ы | Ы2:ы | Cns ] %> х: | %> а х ] _ ;
       # Cns* [ о | э | е ] [ Н1: | Ӈ1: ] %> х: _ [ ʼ | д ] ; 
      # Cns* Vow: Cns+ Vow: (%^VowFronting:0) %> й %> х: _ [ Cns  | ˮ ] ;
!€# хона%>й%>х%{ауоэØ%}нзь
!€0 хонэ0й0ханзь

"%{ауоэØ%} -> а right"
!! __@RULENAME@__
 %{ауоэØ%}:а =>   [Cns* [ а | я | о | у | ю | ы | и ] [ Н1: | Ӈ1: | Д1: ]| %> с %{ая%}: | %> я | а я ] %> х: _ ;
                   [[ [ Cns | # Cns* ] [ а | я | й | и | ы | Ы2: ]| Cns ] %> х: | %> а х ] _ ;
!                   [[ а | я | й | и | ы | Ы2: | Cns ] %> х: | %> а х ] _ ;
       # Cns* [ о | э | е ] [ Н1: | Ӈ1: ] %> х: _ [ ʼ | д ] ; 
       # Cns* [ о | э | е ] [ Н1: | Ӈ1: ] %> х: _ [( ˮ ) н Vow | т Vow ] ; 
      # Cns* Vow: Cns+ Vow: (%^VowFronting:0) %> й %> х: _ [ Cns  | ˮ ] ;
!€# хона%>й%>х%{ауоэØ%}нзь
!€0 хонэ0й0ханзь
!!# Cns* Vow+ Cns* Vow: [[ Ӈ1: | Н1: |  ʼ: | Д1: | С1: ] %> х: | %> а х ] _ ;
!2013-03-15! adding и | ы | й | Ы2: for тёресь+V+Refl+Mod/appr+RcDu3
 # Cns* PAIRSYLL+ %> д _ р е ; !!= @CODE@ even-syllables

"%{аяуюØ%}:а vowel singular accusative after consonant LEFT"
!! __@RULENAME@__
 %{аяуюØ%}:а <= Cns %> _ [ :Cns - :в ] ;

"%{аяуюØ%}:а vowel singular accusative after consonant RIGHT"
!! __@RULENAME@__
 %{аяуюØ%}:а => Cns %> _ :Cns ;

"%{аяуюØ%}:у vowel singular accusative after consonant RIGHT"
!! __@RULENAME@__
 %{аяуюØ%}:у => Cns %> _ :в ;

"%{ауоэØ%} <-> у"
!! __@RULENAME@__
 %{ауоэØ%}:у <=> [ у | ю ]  %> х _ ;

"%{ауоэØ%} -> о"
!! __@RULENAME@__
!! {{{
 %{ауоэØ%}:о <=> [ о | ё ]  (%^VowLower:0) %> [ х | ˮ ] _ ; !!= @CODE@ 2013-10-22 хоˮомна
!! }}}

"%{ауоэØ%} -> э"
!! __@RULENAME@__
 %{ауоэØ%}:э <=> [ Ы2:э | ы:э | э | :е ] (%^VowLower:0) %> х _ ;
!2013-03-15!![ ы | э | е ]  %> х _ ;

"%{ауоэØ%} <-> ы"
!! __@RULENAME@__
 %{ауоэØ%}:ы <=> [ а:ы ] %^A2I: %> х _ ;
             [ ъ | ˮ %> ъ:0 ] и %> х _ ;

"%{ауоэØ%} <-> и"
!! __@RULENAME@__
 %{ауоэØ%}:и <=> [[Vow: | Cns | :ʼ ] %> ъ:0 и | а:и %^A2I: ] %> х _ ;


"%{ауоэиыØ%} <-> а"
!! __@RULENAME@__
 %{ауоэиыØ%}:а <=> # Cns* [ :а | :я ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;

"%{ауоэиыØ%} <-> ы"
!! __@RULENAME@__
 %{ауоэиыØ%}:ы <=> # Cns* [ ы ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;

"%{ауоэиыØ%} <-> и"
!! __@RULENAME@__
 %{ауоэиыØ%}:и <=>  # Cns* [ и ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;

"%{ауоэиыØ%} <-> о"
!! __@RULENAME@__
 %{ауоэиыØ%}:о <=>  # Cns* [ :о | :ё ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;

"%{ауоэиыØ%} <-> у"
!! __@RULENAME@__
 %{ауоэиыØ%}:у <=>  # Cns* [ :ю | :у ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;

"%{ауоэиыØ%} <-> э"
!! __@RULENAME@__
 %{ауоэиыØ%}:э <=>  # Cns* [ :е | э ]  [[ Н1: | Ӈ1: | Д1: ] %> (ˮ)| %> ˮ ] _ %{увм%}: (А2:) н %{ая%}: ;


!! !VOWEL LOWERING
"StemFinalYToE"
!! __@RULENAME@__
 ы:э <=> _ [ %^VowLower:0 %> (ˮ) (%{ауоэиыØ%}:| :э) :CnsNotGlots | (%^VowLower:0) %> [ х | ӈ ] э ]  ;
        
"а:э in final position of polysyllable verb stems"
!! __@RULENAME@__
!! {{{
 а:э <=>  [ ӈ | т | л | р | д | н ] _ [ %^VowFronting:0 | %> й [н|д] ] ;
      Cns  _ %> ъ: [ я: ] [ д | н ] ;
!€# хада>ъян
!€0 хадэ00ян
 !!= @CODE@ хадась+V+TV+Ind+Aor+ScSg1+OcPl3: хадэян
!€# хада%^VowFronting>ъи>нась
!€0 хадэ000й0нась
 !!= @CODE@ хадась+V+TV+Ind+Prt1+ScSg1+OcPl3: хадэйнась
      д _ б а: %^A2I: %> ъ: я: [ д | н ] ;
!! }}}


"Ы1:о"
!! __@RULENAME@__
 Ы1:о <=> LBV [ CnsNotGlots | Glots | :ӈ | :н ] %> _ ;

"Ы1:е"
!! __@RULENAME@__
 Ы1:е <=> LFV [ CnsNotGlots | Glots | :ӈ | :н ]  %> _ ; 

"Ы2:ы"
!! __@RULENAME@__
 Ы2:ы <= :в _ [ ц | с ] ь [ # | %> ] ; 

"a:o in я"
!! __@RULENAME@__
 я:ё <=> _ %^A2O:0 ;

"a:o in та"
!! __@RULENAME@__
 а:о <=> Cns _ %^A2O:0 ;

"я:э in яля"
!! __@RULENAME@__
 я:э <=> _ %^HardFronting:0 ;

"a:i in мараӈгы"
!! __@RULENAME@__
 а:ы <=> [ к | г | х | л ] _ %^A2I:0 ;
!€# яˮавла%^A2I%>дмʼ
!€0 яˮавлы00дмʼ


"a:i in ӈуда"
!! __@RULENAME@__
 а:и <=> [ Cns - [ к | л | х | г ] | %{вм%}: ] _ %^A2I:0 ;
         р в:б  _ %^VowRaise:0 %> ;
!€# хо%>рва%^VowRaise%>н
!€0 хо0рби00н

"о:у word-final position"
!! __@RULENAME@__
 о:у <=> _ %^VowRaise:0 ;

"ё:ю word-final position"
!! __@RULENAME@__
 ё:ю <=> _ %^VowRaise:0 ;

"э:ы word-final position"
!! __@RULENAME@__
 э:ы <=>		_ %^VowRaise:0 ;

"я:и word-final position"
!! __@RULENAME@__
 я:и <=>		_ %^VowRaise:0 ;
!"a:i in пя"
     _ %^A2I:0 ;
     
"а:е хасава:хасев"
!! __@RULENAME@__
 а:е <=> Cns _ Cns а: %^StemVowFronting:0 [ %> | # ] ;

"а:у яˮавла:яˮавлу LEFT"
!! __@RULENAME@__
 а:у <= [ : : ] - [ й:0 %> | ь:0 %> | р [ в:б | а в | х ] ] _ %^VowRaise:0 ;
      [ : : ] - [ й:0 %> | ь:0 %> | р в:б ]   _ %^VowRaise: %> %{вм%}: д а в э й ;

"а:у яˮавла:яˮавлу RIGHT"
!! __@RULENAME@__
 а:у => [ : : ] - [ й:0 %> | ь:0 %> | р [ в:б | а в | х ] ] _ %^VowRaise:0 ;
      [ : : ] - [ й:0 %> | ь:0 %> | р в:б ]   _ %^VowRaise: %> %{вм%}: д а в э й ;
        # Cns* Vow: [ Cns+ Vow: Cns+ Vow: ]* ( %^VowRaise: ) %> д _ %> в [ %> | # ] ;
!€# яˮавла%^VowRaise%>ˮ
!€0 яˮавлу00ˮ

"а:ю яˮавла:яˮавлу"
!! __@RULENAME@__
 а:ю <=> [ в: | б | п ] _ %^VowFronting:0 %^VowRaise:0 ;
!€# пэва%^VowRaise%>ˮ
!€0 пэбю00ˮ


"и:й vowel change in morpheme onset"
!! __@RULENAME@__
 и:й <=> Vow: %^VowFronting: %> ъ: _ (%^A2I:) %> [ н | д ] Vow ;
!€# хада%^VowFronting>ъи%^A2I>нась
!€0 хадэ000й00нась
 !!= @CODE@ хадась+V+TV+Ind+Prt1+ScSg1+OcPl3: хадэйнась

"я:й Stem vowel loss in plural accusative"
!! __@RULENAME@__
 я:й <=> Vow _ ( %^VowFronting: ) %^VowLoss:0 ;
        Vow %> ъ: _ (%>) [ н | д ] Vow ;


!! !!VOWEL LOSS

"а:0 vowel loss in plural accusative and elsewhere LEFT"
!! __@RULENAME@__
 а:0 <= Cns _ ( %^StemVowFronting: ) %^VowLoss:0 ;
        # Cns:* Vow: [ Cns:+  Vow: ]+ %> ӈ: _ х а ʼ ;
        # Cns:* Vow: [ Cns:+  Vow: Cns:+  Vow: ]* (%^VowLower:) %> д _ р е [ м | в ] ;
        Vow: (%^VowRaise:) %> %{вм%}: _ н а ;
           в _ (ʼ:) %> [ %{вм%}: [ Ы2: | э: | ы | а ] | ӈ г у | ˮ м а: | н: %{ая%}: ] ;
        

"а:0 vowel loss in plural accusative and elsewhere RIGHT"
!! __@RULENAME@__
 а:0 => Cns _ ( %^VowFronting: ) %^VowLoss:0 ;
        # Cns:* Vow: [ Cns:+  Vow: ]+ %> ӈ: _ х а ʼ ;
        # Cns:* Vow: [ Cns:+  Vow: Cns:+  Vow: ]* (%^VowLower:) %> д _ р е [ м | в ] ;
        Vow: (%^VowRaise:) %> %{вм%}: _ н а ;
           в _ (ʼ:) %> [ %{вм%}: [ Ы2: | э: | ы | а ] | ӈ г у | ъ и | с: %{ая%}: | б ц | ˮ м а: | н: %{ая%}: ] ;
        # Cns:* Vow: [ Cns:+  Vow: Cns:+  Vow: ]* (%^VowLower:) %> д _ %> [ д а | м и ʼ | р и ʼ | р а ˮ | д и ʼ | д о ʼ | в а ˮ | н и ʼ | н а ˮ ] ;
        

"я:ь Stem vowel loss in plural accusative"
!! __@RULENAME@__
 я:ь <=> Cns _ ( %^VowFronting: ) %^VowLoss:0 ;
        %> с _ %> ˮ [ # | %> ] ;
!€# хона>ся>ˮ
!€0 хона0сь0ˮ

"я:0 Stem vowel loss in plural accusative"
!! __@RULENAME@__
 я:0 <=> _ %^PalLoss:0 %^VowLoss:0 ;
         а: %^A2I:0 %> ъ: _ (%>) [ н | д ] ;

"и:ь Refl Mod/int"
!! __@RULENAME@__
 и:ь <=>         %> с _ %> ˮ [ # | %> ] ;
!€# тёре>си>ˮ
!€0 тёре0сь0ˮ

"%{аяуюØ%}:0 Stem vowel loss in singular accusative after vowel stem"
!! __@RULENAME@__
 %{аяуюØ%}:0 <=> :Vow %> _ ;


!! !!CONSONANT CHANGES

"mToV"
!! __@RULENAME@__, perhaps there should be a VtoM rule2013-01-23
 м:в <=> _  %^MLenition:0 [ # | %> [ о |  м | ʼ | ˮ ] ] ;
        _ ʼ:0  %^MLenition:0 %>  0:  %{увм%}: А2: ;
       :Vow %^MLenition:0 %> _ [ э к | э р х | Ы2: | а н д а р х | а н | А2: н а ] ;

"%{увм%} ToV"
!! __@RULENAME@__,
 %{увм%}:в <=>
        :Vow ( %^VowLower: ) %> (%{ауоэиыØ%}:)  _  А2: н [ а | %{ая%}: ]  ;
        н %> 0:у  _  А2: н а  ;
!       :Vow %^MLenition:0 %> _ [ э к | э р х | Ы2: | а н д а р х | а н | А2: н а ] ;
!       [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> 0: _  А2: н а  ;
!ʼ %> [ Ы1: | ʼ | м ʼ ] ;


"%{увм%} ToM"
!! __@RULENAME@__,
 %{увм%}:м <=>
        [ :Vow |Vow: ] ( %^VowLower: | %^VowRaise: | %^A2I: | ( %^PalLoss: ) %^VowLoss: ) %> [ ˮ | Д1: ]  _  А2: н а  ;
        [ Ӈ1: | Д1: ] %> _  А2: н а  ;
        [[ Ӈ1: | ʼ: | ˮ: | Д1: | С1: ] %> | %> ˮ ] %{ауоэиыØ%}: _ (А2:) н %{ая%}: ;

"mTob" 
!! __@RULENAME@__
 м:б <=> _ %^MLenition:0 %> [ е ] ;


"в To б "
!! __@RULENAME@__
 в:б <=> [ %> р | Vow ] _ а: (%^VowFronting:0) %^VowRaise: %> ;
         Vow _ а: %^A2I: ;

"%{вм%}:б"
!! __@RULENAME@__
 %{вм%}:б <=> [ %> р | Vow ] _ а: (%^VowFronting:0) %^VowRaise: %> ;
         Vow (%>) _ а:и %^A2I: ;
!€# ни%>%{вм%}а%^A2I
!€0 ни0би0

"v To m "
!! __@RULENAME@__
 в:м <=> [ Ӈ1: ] %> _ ;
! [ м: ʼ: | Ӈ1: ] %> _ ;

"r To l after nasal glottal"
!! __@RULENAME@__
 р:л <=> [ ʼ: | Ӈ1: ] %> _ ;
        Vow _  %> р: [ в | х ] а ;

"r To 0 after л glottal"
!! __@RULENAME@__
 р:0 <=> Vow [ л | р: ] %> _  ;


"nTod"
!! __@RULENAME@__
 н:д <=>  [ Ӈ1: | Н1: | ʼ: ] %> _ [ ʼ | а | %{ая%}: ] ;

"tTod"
!! __@RULENAME@__
 т:д <=>   Н1: %> _ а ;

"n to t after non-nasal glottal"
!! __@RULENAME@__
 н:т <=>  [ С1: | Д1: | ˮ: ] %> _ [ ʼ | а  ] ;

"d to t after non-nasal glottal"
!! __@RULENAME@__
 д:т <=> [ С1:0 | Д1:0 | ˮ:0 ] %> _ ;

"d to l after non-nasal glottal"
!! __@RULENAME@__
 д:л <=> [ Д1:ˮ | ˮ:ˮ  ] %> _ а ц ь ;


"b to p after non-nasal glottal"
!! __@RULENAME@__
 б:п <=> [ С1: | Д1: | ˮ: ] %> _ ;
!        %> _ ˮ # ; илесь:илебˮ

"b to 0 between mod/obl"
!! __@RULENAME@__
 б:0 <=> [ л | р ] %> _ ц [ а к | у ];


"ъ:0 with Conj after vowels"
!! __@RULENAME@__
!! {{{
 ъ:0 <=> Vow: (%^VowFronting:) %> _ [ и: | я: ] ;
         :Vow (ˮ|%^A2I:) %> _ я: (%>) [ н | д ] ; !!= @CODE@ нэкалць+V+TV+Ind+Prt1+ScPl1+OcPl3: нэкалъянаць
!! }}}


"с => ц affrication after Stop"
!! __@RULENAME@__
 с:ц <=> [ С1: | Д1: | ˮ: | л ] %> _ ь ;
        [ л | р ] %> _ [ а | я | и | %{ая%}: ] ;

"с => з after nasal"
!! __@RULENAME@__
 с:з <=> [ м | н | Н1: | Ӈ1: | ʼ: ] %> _ ;

"ʼ:н"
!! __@RULENAME@__
 ʼ:н <=> [ д | н ] [ о | и ] _ %> с: ь ;

"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=> м:  _ %> [ Ы1: | ʼ | м ( ʼ ) | н: ( ʼ ) | ˮ: | х: ( %{ауоэØ%}: ) | р: | в: | д | ц ь | с: ь ] ;
       м:  _ %^MLenition:0 %> 0:у м: А2: н а ;
    _ %> [ %{рл%}: | %{вм%}: | н: ] ;

"ˮ:0"
!! __@RULENAME@__
 ˮ:0 <=> _ %> д: ;
!2014-02-04!     %> б:п _ ;
    _ %> [ %{рл%}: | %{вм%}: ] ;
    _ %> с: ь ;

"х -> г after nasal glottal"
!! __@RULENAME@__
 х:г <=> [ ʼ: | Ӈ1: | Н1: | м ] %> _ ( %{ауоэØ%}: ) ;

"х to к after non-nasal glottal"
!! __@RULENAME@__
 х:к <=> [ Д1: | С1: | л | р ] %> _  ;

!! !!Realization of glottal stops
!! !SURFACE CONSONANT

"%{вм%}:м"
!! __@RULENAME@__
 %{вм%}:м <=> [ Ӈ1: | Н1: | Д1: | С1: | ʼ: | ˮ: | р | л ] %> _  ;

"%{рл%}:л"
!! __@RULENAME@__
 %{рл%}:л <=> [ Ӈ1: | Д1: | С1: | Н1: | ʼ: | ˮ: ] %> _  ;

"%{вм%}:в"
!! __@RULENAME@__
 %{вм%}:в <=> [ :Vow | в Vow: | [ Cns: ] - [ р | л ] ] (%^VowLower:0|%^VowRaise:0) %> _  [ # | %> |  Cns: | [ Vow: ] - [ а:и ] ] ;
              [ в а: (%^VowRaise:0)| Vow: | е |  т ] %> _ [ Ы2: | э: | ы | а | д а в ] ;
        %> %{аяуюØ%}: _ ; 

"%{рл%}:р"
!! __@RULENAME@__
 %{рл%}:р <=> [ :Vow | Cns: ] (%^VowLower:0) %> _  ;



"С1:с GlottalNonNasalToSurface"
!! __@RULENAME@__
 С1:с <=> _ %> [ м #: | ʼ #: | ˮ #: | (0:о) ˮ м а | Ы1: ] ;

"Д1:д GlottalNonNasalToSurface"
!! __@RULENAME@__
 Д1:д <=> Vow: _ %> [ м ( ʼ ) #: | ʼ #: | ˮ [ # | ӈ э ] | о | е | ё ] ;

"Н1:н GlottalNasalToSurface"
!! __@RULENAME@__
 Н1:н <=> _ %> [ Ы1: | с: ь | т: | д: | ˮ | м: д [ # | %> | а | и ] | м ʼ | о | ʼ | н: ʼ ] ;

"Н1:ӈ GlottalNasalToSurface"
!! __@RULENAME@__
 Н1:ӈ <=> _ %> [ х: ] ;

"Ӈ1:ӈ GlottalNasalToSurface"
!! __@RULENAME@__
 Ӈ1:ӈ <=>  _ %> [ Ы1: | х: | о ] ;

"Ӈ1:н GlottalNasalToSurface"
!! __@RULENAME@__
 Ӈ1:н <=>  _ %> [ с: | н:д | д | ц: | ˮ | м: д ] ;

"Ӈ1:й GlottalNasalToSurface"
!! __@RULENAME@__
 Ӈ1:й <=> о _ %> [ ʼ | м ʼ  ] ;


!! !LOSS

"л:0"
!! __@RULENAME@__
 л:0 <=> _ [ ʼ: | ˮ: ] %> %{рл%}:л ;

!"v loss after m Nasal glottal"
!в:0 <=> м ʼ: %> _ #: ;

"m loss before Labial followed by nonglottal cons or vows "
!! __@RULENAME@__
 м:0 <=> _ ʼ: %> [ м [ а | и | н | д ] | в: ] ;
       [ Ӈ1: | Н1: ] %> _ д ; !! * @CODE@ поʼ+N+Sg+Acc+PxSg2:понд 

"ӈ:0 NOT мэсь+V+IV+Ind+Aor+ScDu3: мэӈахаʼ"
!! __@RULENAME@__
 ӈ:0 <=> # Cns:* Vow: [ Cns:+  Vow: ]+ %> _ а: х а ʼ ;


!"Н1:0 GlottalNasalToSurface"
!Cx:0 <=> _ %> [ м | ʼ ] ;
!       where Cx in (Н1 Ӈ1) ;

"Д1:0 GlottalNonNasalToZERO"
!! __@RULENAME@__
 Д1:0 <=> _ %>  [ х: | н:  ʼ | с: ь | ц ь | д: (а ˮ) | д:т а ц ь | д: м ʼ | д: а м з ь | д: и н з ь ]  ;
 Cns:  _ %>  [ (м) ʼ | ˮ ]  ;

"С1:0 GlottalNonNasalToZERO"
!! __@RULENAME@__
 С1:0 <=> _ %> [ х: | н: | б: | с: ь | т ˮ ] ;


"ь:0 with  GlottalNonNasalToZERO"
!! __@RULENAME@__
 ь:0 <=> С1: _ %> [ х: | н ] ;
     _ %> [ а: | у: | и ] ;
!!

"С1:ˮ UnderlyingToGlottal"
!! __@RULENAME@__
 С1:ˮ <=> _ [ #: | %> м н а | %> %{вм%}:м ] ;

"Д1:ˮ UnderlyingToGlottal"
!! __@RULENAME@__
 Д1:ˮ => _ [ #: | %> [ д:л а ц ь | и | м и | ӈ э | м а | а м н а | ( 0: ) м н а | н а | н [ и | # | %> ] ] ] ;

"Н1:ʼ UnderlyingToGlottal"
!! __@RULENAME@__
 Cx:ʼ <=> _ #: ;
       where Cx in (Н1 Ӈ1) ;

