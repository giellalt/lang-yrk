! Nenets twol file

Alphabet

! Letters of the alphabet
а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я
А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

 ˮ ! Non-nasal glottal
 ʼ ! Nasal glottal

! other symbols
   %- !0

! Archiphonemes for vowels
А1:а
А1:у
А1:о
А1:э

А2:а
!А2:0

Ы1:о 
Ы1:е

Ы2:э


! Archiphonemes for glottals
! Н1:ʼ
! Н1:н
Н1:0
! Ӈ1:ʼ
! Ӈ1:ӈ
 Ӈ1:0
 Д1:ˮ
 Д1:д
 Д1:0
 С1:ˮ
 С1:с
 С1:0

!! !!triggers
%^A2O:0
%^A2I:0 
%^MLenition:0 !! lenition
%^VowRaise:0 !! vowel raising э:ы о:у
%^VowLoss:0 !! stem-final vowel is lost in plural accusative
%^VowFronting:0 !! хасава:хасев
%^PalLoss:0 !! in combination with stem-final vowel loss тёня:тён
%^HardFronting:0 !! яля:ялэ
! Boundary symbols

 %>
 #

 ;



Sets
Vws =   а е ё и о у ы э ю я
        А Е Ё И О У Ы Э Ю Я ;

CnsNotGlots = б в г д ж з й к л м н п р с т ф х ц
              Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц
              ч ш щ
              Ч Ш Щ  ;
Glots = ˮ ʼ ;

Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;
Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш С ;

Ltrs = Vws Cns  ъ ь Ъ Ь ;

all =  Ltrs  %- ;




Definitions

LFV = [ е | ю | ё | и ] ;

LBV = [ а | я | [ #: | CnsNotGlots ] [ у | о | э | ы ] ] ;


Rules

"0:а between Д1: %> and  мна"
0:а <=> Д1: %> _ м н а ;

"й:0 when followed by vowel in VowelPalatalization"
й:0 <=> _ %>: [ а: | у: ] ;

"а:я VowelPalatalization"
а:я <=> й:0 %>: _ ;

"у:ю VowelPalatalization"
у:ю <=> й: %>: _ ;

"А2:0 possible vowel loss as second syllable vowel"
А2:0 <= # Cns* :Vws [ %> | Cns | ˮ ]+ м: _ н а ; 
         :Vws %^MLenition:0 %> м: _ н а ; 
         [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> 0: м: _ н а ; 

"А2:0 possible vowel loss as second syllable vowel"
А2:0 =>  :Vws %^MLenition:0 %> м: _ н а ; 
         [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> 0: м: _ н а ; 

"0:U in sgprossuf"
0:у <=> [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> _ м: А2: н а ;

"А1 -> а"
А1:а <=> [[ Ӈ1: |  ʼ: | а | я | й | и | ы | Ы2: | Cns | Д1: | С1: ] %>: х: | %> а х ] _ ;
!2013-03-15! adding и | ы | й | Ы2: for тёресь+V+Refl+Mod/appr+RcDu3

"А1 -> у"
А1:у <=> [ у | ю ]  %> х _ ;

"А1 -> о"
А1:о <=> [ о | ё ]  %> х _ ;

"А1 -> э"
А1:э <=> [ э | е ]  %> х _ ;
!2013-03-15!![ ы | э | е ]  %> х _ ;


"StemFinalYToE"
ы:э <=> _ %>: х э  ;

"а:э in final position of polysyllable verb stems"
а:э <=>  [ ӈ | т | л | р | д | н ] _ %^VowelFronting:0 ;

"Ы1:о"
Ы1:о <=> LBV [ CnsNotGlots | Glots | :ӈ | :н ] %>: _ ;

"Ы1:е"
Ы1:е <=> LFV [ CnsNotGlots | Glots | :ӈ | :н ]  %>: _ ; 

"Ы2:ы"
Ы2:ы <= в _ [ ц | с ] ь [ # | %>: ] ; 

"a:o in я"
я:ё <=> _ %^A2O:0 ;

"a:o in та"
а:о <=> Cns _ %^A2O:0 ;

"я:э in яля"
я:э <=> _ %^HardFronting:0 ;

"a:i in мараӈгы"
а:ы <=> [ к | г ] _ %^A2I:0 ;

"a:i in ӈуда"
а:и <=> [ Cns - [ к | г ] ] _ %^A2I:0 ;

"о:у word-final position"
о:у <=> _ %^VowRaise:0 ;

"ё:ю word-final position"
ё:ю <=> _ %^VowRaise:0 ;

"э:ы word-final position"
э:ы <=>		_ %^VowRaise:0 ;

"я:и word-final position"
я:и <=>		_ %^VowRaise:0 ;
!"a:i in пя"
     _ %^A2I:0 ;
     
"а:е хасава:хасев"
а:е <=> Cns _ Cns а: %^VowFronting:0 ;

!! Vowel loss
"а:0 Stem vowel loss in plural accusative"
а:0 <=> Cns _ (%^VowFronting) %^VowLoss:0 ;

"я:ь Stem vowel loss in plural accusative"
я:ь <=> Cns _ (%^VowFronting) %^VowLoss:0 ;

"я:й Stem vowel loss in plural accusative"
я:й <=> Vws _ (%^VowFronting) %^VowLoss:0 ;

"я:0 Stem vowel loss in plural accusative"
я:0 <=> _ %^PalLoss:0 %^VowLoss:0 ;



!! Consonant changes

"mToV" ! perhaps there should be a VtoM rule2013-01-23
м:в <=> _  %^MLenition:0 [ # | %> [ о |  м | ʼ | ˮ ] ] ;
        _ ʼ:0  %^MLenition:0 %>  0:  м: А2: ;
       :Vws %^MLenition:0 %> _ [ э к | э р х | Ы2: | а н д а р х | а н | А2: н а ] ;
       [ д | м: (ʼ: ) | н | р ] %^MLenition:0 %> 0: _  А2: н а  ;
!ʼ %>: [ Ы1: | ʼ | м ʼ ] ;

"mTob" ! 
м:б <=> _ %^MLenition:0 %> [ е ] ;


"v To m "
в:м <=> [ Ӈ1: ] %>: _ ;
! [ м: ʼ: | Ӈ1: ] %>: _ ;

"r To l after nasal glottal"
р:л <=> [ ʼ: | Ӈ1: ] %>: _ ;
        Vws _  %>: р: [ в | х ] а ;

"r To 0 after л glottal"
р:0 <=> Vws [ л | р: ] %>: _  ;


"nTod"
н:д <=>  [ Ӈ1: | ʼ: ] %>: _ ʼ ;

"n to t after non-nasal glottal"
н:т <=>  [ С1: | Д1: | ˮ: ] %>: _ [ ʼ | а к  | а р ] ;

"d to t after non-nasal glottal"
д:т <=> [ С1: | Д1: | ˮ: ] %>: _ ;

"b to p after non-nasal glottal"
б:п <=> [ С1: | Д1: | ˮ: ] %>: _ ;
        %> _ ˮ # ;

"b to 0 between mod/obl"
б:0 <=> [ л | р ] %>: _ ц [ а к | у ];


"ъ:0 with Conj after vowels"
ъ:0 <=> Vws %>: _ и ;

"с => ц affrication after Stop"
с:ц <=> [ Д1: | л ] %>: _ ь ;
        [ л | р ] %>: _ [ а | я | и ] ;

"с => з after nasal"
с:з <=> [ м | н | Н1: | Ӈ1: | ʼ: ] %>: _ ;

"ʼ:0"
ʼ:0 <=> м:  _ %>: [ Ы1: | ʼ | м ( ʼ ) | н: ( ʼ ) | ˮ: | х: ( А1: ) | р: | в: | д | ц ь | с: ь ] ;
       м:  _ %^MLenition:0 %>: 0:у м: А2: н а ;

"ˮ:0"
ˮ:0 <=> _ %>: д: ;
     %>: б:п _ ;

"х -> г after nasal glottal"
х:г <=> [ ʼ: | Ӈ1: | Н1: | м ] %>: _ ( А1: ) ;

"х to к after non-nasal glottal"
х:к <=> [ Д1: | С1: ] %>: _  ;

! Realization of glottal stops
!! SURFACE CONSONANT
"С1:с GlottalNonNasalToSurface"
С1:с <=> _ %>: [ м #: | ʼ #: | ˮ #: | Ы1: ] ;

"Д1:д GlottalNonNasalToSurface"
Д1:д <=> _ %>: [ м ( ʼ ) #: | ʼ #: | ˮ #: | о | е | ё ] ;

"Н1:н GlottalNasalToSurface"
Н1:н <=> _ %>: Ы1: ;

"Ӈ1:ӈ GlottalNasalToSurface"
Ӈ1:ӈ <=> _ %>: [ Ы1: | х: | о ] ;

"Ӈ1:н GlottalNasalToSurface"
Ӈ1:н <=> _ %>: [ с: | н:д | д | ц: | ˮ ] ;


!! LOSS

!"v loss after m Nasal glottal"
!в:0 <=> м ʼ: %>: _ #: ;

"m loss before Labial followed by nonglottal cons or vows "
м:0 <=> _ ʼ: %>: [ м [ а | и | н | д ] | в: ] ;

!"Н1:0 GlottalNasalToSurface"
!Cx:0 <=> _ %>: [ м | ʼ ] ;
!       where Cx in (Н1 Ӈ1) ;

"Д1:0 GlottalNonNasalToZERO"
Д1:0 <=> _ %>:  [ х: | н:  ʼ | с: ь | ц ь | д: а ˮ | д: а ц ь | д: м ʼ | д: а м з ь | д: и н з ь ]  ;

"С1:0 GlottalNonNasalToZERO"
С1:0 <=> _ %>: [ х: | н: | б: | с: ь ] ;


"ь:0 with  GlottalNonNasalToZERO"
ь:0 <=> С1: _ %>: [ х: | н ] ;
!!

"С1:ˮ UnderlyingToGlottal"
С1:ˮ <=> _ [ #: | %>: м н а ] ;

"Д1:ˮ UnderlyingToGlottal"
Д1:ˮ <=> _ [ #: | %> [ и | м и | ӈ э | м а | ( 0: ) м н а | н а | н [ и | # | %>: ] ] ] ;

"Н1:ʼ UnderlyingToGlottal"
Cx:ʼ <=> _ #: ;
       where Cx in (Н1 Ӈ1) ;

